"The Quarrel" by Zane Mariano & Jon Gill

Include Basic Screen Effects by Emily Short.
Include Reactable Quips by Michael Martin.
Release along with an interpreter, the source text and a website.
Use no scoring.

Chapter I - Values

[Data]
Genre_value is a number that varies.

Chapter II - Actions
	
[FASTFORWARD Action]	
Fastforwarding is an action applying to nothing.
Understand "do_fastforward" as fastforwarding.

move_to_room is a room that varies.
Instead of fastforwarding:
	let is_next_room be false;
	if location is solved_noir:
		let is_next_room be whether or not there is a next_room_noir corresponding to a curr_room of the location in the table of room connections;
		if is_next_room is true:
			now move_to_room is the next_room_noir corresponding to a curr_room of the location in the table of room connections;
		otherwise:
			say "[BLUE LETTERS]Nowhere to go.[BLACK LETTERS]";
	otherwise if location is solved_romance:
		let is_next_room be whether or not there is a next_room_romance corresponding to a curr_room of the location in the table of room connections;
		if is_next_room is true:
			now move_to_room is the next_room_romance corresponding to a curr_room of the location in the table of room connections;
		otherwise:
			say "[RED LETTERS]Drat!  There's nowhere for you to go.[BLACK LETTERS]";
	otherwise:
		say "[RED LETTERS]There's still work to be done here.[BLACK LETTERS]";
	if is_next_room is true:
		move the player to move_to_room, without printing a room description;
		try resetting;
		move the player to move_to_room;
		

[REWIND Action]
Rewinding is an action applying to nothing.
Understand "rewind" as rewinding.

prev_room_trans is text that varies.
Instead of rewinding:
	let is_prev_room be false;
	let is_prev_room be whether or not there is a prev_room corresponding to a curr_room of the location in the table of room connections;
	if is_prev_room is true:
		say the description corresponding to a curr_room of the location in the table of rewind descriptions;
		if the player consents:
			now move_to_room is the prev_room corresponding to a curr_room of the location in the table of room connections;
			move the player to move_to_room, without printing a room description;
			try resetting;
			move the player to move_to_room;
	otherwise:
		say "[BLUE LETTERS]No... No turning back now. No more running.[BLACK LETTERS]";
	

[RESET Action]
Resetting is an action applying to nothing.
Understand "test_reset" as resetting.
	
[Override RESET for each specific room.]


[Ending the game]
Ending_text_room is a room that varies.
After resetting when the location is ending_room:
	now ending_text_room is the location;
	the fat lady sings in the ending_turns of the location - 1 turns from now.

Every turn when the location is ending_text_room:
	say "[the ending_text of ending_text_room]";
	
At the time when the fat lady sings:
	end the game saying "THE END".
	

[--------------- Player Action Descriptions ---------------]


[UTILIZE]
[Utilize is the version of Use that applies to a single object]
Utilizing is an action applying to one visible thing.
Understand "utilize [something]" as utilizing.
Understand "use [something]" as utilizing.

[USE]
[Use affects one object with another]
Using is an action applying to two visible things.
Understand "use [something] on [something]" as using.
Understand "use [something] with [something]" as using.
Understand "apply [something] to [something]" as using.

[HELP]
Helping is an action applying to nothing. Understand "help" as helping.
After helping when genre_value is less than 0:
	say "[BLUE LETTERS] Time to EXAMINE myself.  Really take a deep LOOK inside.  I was a hard man, with hard convictions.  I'd USE and TAKE, but I'd rarely ASK before I did.[BLACK LETTERS]";
After helping when genre_value is 0:
	say "This is generic help text.";
After helping when genre_value is greater than 0:
	say "This is romantic help text.";


[EXAMINE ME]

Instead of examining player when genre_value is less than 0: say "Noir description."
Instead of examining player when genre_value is 0: say "Generic description."
Instead of examining player when genre_value is greater than 0: say "Romance description."


[GO N/S/E/W]

Instead of going when genre_value is less than 0: say "[BLUE LETTERS]Yes, part of me wanted to run.  But this was where I had to be. [BLACK LETTERS]".
Instead of going when genre_value is 0: say "[RED LETTERS]Oh, bother!  You still have [BLUE LETTERS]work to do.  Messy work.[BLACK LETTERS]".
Instead of going when genre_value is greater than 0: say "Romance going."

[Instead of doing something other than looking, examining, asking, telling, or going, say "HAHAHA NO."]

Chapter III - Conversations


[TALKING Action]

Talking is an action applying to one thing.
Understand "talk [something]" and "talk to [something]" as talking.
Check talking: try asking the noun about "the_default".


[--------------- Conversation Content -- Can be continued in other Chapters ---------------]


	Table of Ask Results (continued)
  	NPC  		topic  					result  
  	Charles	  	"his face" or "ugliness"			hate my face
	Charles		"the_default"				want to die

	Table of Tell Results (continued)
  	NPC  		topic			  		result  
  	Charles  		"sucks"				  	hate my face

	Table of Quip Texts (continued)
  	quip  		quiptext  
  	hate my face	"Yeah, it kind of sucks.  I'm pretty hideous."
	want to die	"You're right.  I should kill myself."

	Table of Quip Followups (continued)
  	quip  		option  					result  
  	hate my face  	"Life isn't really worth living for you."		want to die
  	hate my face  	"You're freaking hideous man."  		want to die

[Quip Results]

	After quipping when the current quip is want to die: end the story saying "Charles goes off to hang himself.  Rough.".

Charles is a person.  The knife is an object.

Chapter IV - Rooms

[Room state]
A room can be unsolved, solved_noir, or solved_romance.  A room is usually unsolved.
Rooms have a text called ending_text.
Rooms have a number called ending_turns.
Rooms can be ending_room. A room is usually not ending_room.

[Room Relations]
	Table of Room Connections
	curr_room		next_room_noir	next_room_romance		prev_room
	Intro			NoirScene1		RomScene1				--
	NoirScene1		NoirScene2		--						Intro
	NoirScene2		NoirScene3a		NoirScene3b				NoirScene1
	RomScene1		--				RomScene2				Intro
	RomScene2		RomScene3b		RomScene3a				RomScene1				
	
	Table of Rewind Descriptions
	curr_room		description
	Intro			--
	NoirScene1		"[BLUE LETTERS] Like the Stygian Void, my memories are a whirlpool of uncertainty. Perhaps we should revisit them once more.[BLACK LETTERS]"
	NoirScene2		"[BLUE LETTERS] I feel close to the end, but as my fear overwhelms me - I can't help but wonder if that was really how it all happened. Shall we try again?[BLACK LETTERS]"
	RomScene1		"[RED LETTERS]Wait, don't — [BLUE LETTERS]I shook the quaint, weirdly English daydream from my head.  Clearly I'd been hitting the pills too hard again.  Now, where was I?[BLACK LETTERS]"
	RomScene2		"[RED LETTERS]That's not quite how it happened, though, was it?  Let's try that last bit again…[BLACK LETTERS]"

[Intro Scene]
Intro is unsolved.

Intro is a room with printed name "The Closet: Trapped".
Understand "The Closet" and "Closet" as Intro.

The description of Intro is "[one of]
	[BLUE LETTERS]I find myself locked in a closet. You'd be surprised how often someone like me finds themselves in these sort of situations after dealing with gangsters and thugs too soft to land the killing blow. [RED LETTERS]That's a bit rich.  More often you find yourself in situations like this when Jenkins from third floor locks the door behind you while you're looking for a fresh pen.[BLUE LETTERS] They'd just as soon lock me away and hope the chemical fumes finish the job. Too bad for them, because escaping locked rooms was my speciality. [RED LETTERS] It certainly wasn't your work.  'Tardy, easily distracted, and poor with numbers,' your last performance review had read. [BLUE LETTERS]The closet was an archive closet where my secretary would store all the old case files from my brilliant triumphs and utter failures. [RED LETTERS]Actually, it smelled more like the janitorial supply closet.  Lovely. [BLUE LETTERS]Don't bother trying the door-knob though, it was [RED LETTERS]locked tight.[BLACK  LETTERS]
	[or][BLUE LETTERS]The closet is as dark and stifling as a coffin,[RED LETTERS] with a wet, vaguely antiseptic smell about it. [BLACK LETTERS][stopping]"

Carry out resetting in Intro:
	[----RomItems----]
	now the filing cabinet is closed;
	now the filing cabinet is not pushed;
	now the crumpled paper is off-stage;
	now the crumpled paper is not written on;
	now the basket is in Intro;
	now the mop is in Intro;
	[----NoirItems----]
	now Boots is in Intro;
	now Flimsy Doorknob is in Intro;
	now Gun Safe is in Intro;
	now Loaded Gun is in Gun Safe;
	
[---- Romance Solution for Intro ----]
The filing cabinet is a container in Intro. 
The filing cabinet is not portable.
The filing cabinet is closed and openable.
The filing cabinet can be pushed.  The filing cabinet is not pushed.
The basket is a container in Intro.
The description of the basket is "[RED LETTERS]Although it was intended to store pens, it has long-since been emptied by the sinister hand of petty corporate theft.[BLACK LETTERS]".
A crumpled paper is a thing.
The description of the crumpled paper is "A crumpled sheet of paper".
The paper can be written on.  The paper is not written on.
The dirty mop is a thing in Intro.
The description of the mop is "[RED LETTERS]The janitor's mop.  Ragged, splintery, and covered in some kind of viscous black goop.[BLACK LETTERS]".
Goop is a part of the mop.  Understand "slime" and "goo" and "gunk" as goop.
The description of the goop is "[RED LETTERS]An disgusting, inky slime coating the fibers of the mop.[BLACK LETTERS]".
The closet door is an object in Intro. It is fixed in place.
The description of the closet door is "[RED LETTERS]Heavy, wooden, entirely unyielding.  Much like your boss when you asked for that raise. Light spills in through a tiny crack along the bottom of it.[BLACK LETTERS]".
The crack is part of the closet door.

Carry out examining filing cabinet:
	if filing cabinet is closed, say "[BLUE LETTERS]Closed, like the door to this closet.[BLACK LETTERS]" instead;
	if filing cabinet is open, say "[BLUE LETTERS]Nothing in here but my hopes and dreams.  Which is to say, nothing in here.[BLACK LETTERS]". 
Instead of pulling filing cabinet:
	say "[RED LETTERS]Despite your thrice-weekly squash practices, you are unable to get a strong enough grip to pull the cabinet towards you.[BLACK LETTERS]" instead.
Instead of pushing filing cabinet:
	if the filing cabinet is not pushed:
		say "[RED LETTERS]You set your back against the cabinet, brace your legs against the wall, and give it the old college try.  You think you feel a blood vessel pop in your forehead, but you succeed in moving the cabinet a few inches.  As you regather your breath, you notice that a small piece of paper is sticking out from behind the cabinet.[BLACK LETTERS]";
		now the crumpled paper is in Intro;
		now the filing cabinet is pushed;
	otherwise:
		say "[RED LETTERS]You give the cabinet another push, wondering what else you might find behind it.  A tenner maybe?  Despite your hopes, though, the cabinet refuses to yield further ground.[BLACK LETTERS]".

Carry out examining crumpled paper:
	if crumpled paper is not written on:
		say "[RED LETTERS]Somebody's typewritten notes.  Some rot about a case or a dame or something.  Had Jenkins been working on his novel during work hours again?[BLACK LETTERS]" instead;
	otherwise:
		say "[RED LETTERS]A ratty piece of paper with the word HELP scrawled across it in stinking mop goo.[BLACK LETTERS]" instead.

Carry out taking the mop:
	say "[RED LETTERS]You stifle a yelp as a splinter a centimeter long embeds itself in your palm.[BLACK LETTERS]."
				
Carry out pushing the closet door: 
	say "[RED LETTERS]You press your shoulder against the wood and heave.  You achieve nothing more than a stiff back.[BLACK LETTERS]" instead.
Carry out opening the closet door: 
	say "[RED LETTERS]You give the handle a frustrated jiggle, but it is definitely locked.[BLACK LETTERS]" instead.
Carry out examining the crack: 
	say "[RED LETTERS]It's wide enough to slip something through, like a note or ribald birthday card.[BLACK LETTERS]" instead.

Check attacking closet door:
	say "[RED LETTERS]You would, but you don't want to bruise your hands.  That door looks hard.[BLACK LETTERS]" instead.
	
Check using:
	if the noun is mop:
		try utilizing the noun instead;
	if the noun is goop and the second noun is crumpled paper:
		now crumpled paper is written on;
		say "[RED LETTERS]You suppress a shudder as you rub the tarry substance over the paper.  When you are done your hands are stained and the word HELP is scrawled across the sheet, crude and malodrous.[BLACK LETTERS]" instead;
	if the noun is crumpled paper and the second noun is crack:
		if crumpled paper is not written on:
			say "[RED LETTERS]You begin to slide the paper under the door, but realize that sharing someone else's notes with the wider world will not do much to aid you in your current predicament.  You withdraw the paper from the crack.[BLACK LETTERS]" instead;
		otherwise:
			say "[RED LETTERS]You slide your desperate missive through the crack beneath the door.  After a few minutes, you hear someone walk towards the door, pick up the note, and pause.  A few seconds later, the latch clicks, the door opens, and you are blinded by the bright light that spills into the closet from outside.[BLACK LETTERS]";
			now Intro is solved_romance;
			try fastforwarding instead;
	if the noun is crumpled paper and crumpled paper is not written on:
		try utilizing crumpled paper instead.

Check utilizing:
	if the noun is mop:
		say "[RED LETTERS]There's nothing to clean.  Besides, that goop on the end of it would probably leave more of a mess than was already there.[BLACK LETTERS]" instead;
	if the noun is crumpled paper:
		say "[BLUE LETTERS]Just ancient history now.[BLACK LETTERS]".

[ ----- Noir Solution for Intro ----- ]
	
The pair of boots are a thing in Intro. The description is "[BLUE LETTERS]So this is where I left my trusty pair of boots. Kind of a strange place, but hey - they could come in handy. Just looking at them reminds me of the good times we had together, kicking down doors to gangster's houses and the such.[BLACK LETTERS]".
The flimsy doorknob is a thing in Intro. The description is "[BLUE LETTERS]When you run a PI firm, you don't really have the kind of money to fix doorknobs.  This one has always been especially flimsy.[BLACK LETTERS]".
The flimsy doorknob is fixed in place.
The gun safe is a container in Intro. The description is "[BLUE LETTERS]Ahhh, the gun safe. I never kept it locked because honestly, if someone broke into my office - I deserve to be shot. Better to keep it unlocked and grab that loaded revolver in times of emergency.[BLACK LETTERS]".
The gun safe is openable, closed, opaque and fixed in place.
The loaded gun is a thing in the gun safe. The description is "[BLUE LETTERS]The cold steel of the revolver is something all too familiar to my palm. While this one isn't my main weapon, she's seen me through some tough times all the same. I always keep her clean, freshly greased and ready to blow.[BLACK LETTERS]".

Check using: 
	if the noun is pair of boots and the second noun is the flimsy doorknob:
		say "[BLUE LETTERS]Strapping the sturdy boots on my feet, I feel revitalized. With a quick heave and hoe, that flimsy doorknob shatters straight off the door. That's about enough excitment for one day, best to strap down with a victory whiskey in my office. But I guess there's still Celine to find...[BLACK LETTERS]";
		now Intro is solved_noir;
		try fastforwarding instead;
	if the noun is the loaded gun and the second noun is the flimsy doorknob:
		say "[BLUE LETTERS]I look at the gun in my hand, and then at the doorknob. It seems like a simple solution, but probably not the greatest. I'm gonna need a big glass of whiskey to get rid of the banging headache that this is sure to give me. But fuck it, I've got to get out of there somehow... [line break][line break]*BANG*[line break][line break] The revolver explodes with a bright light, smoke, and a painful ringing in my ears. Obviously, there's no evidence left of the doorknob's once residence on the door. Time to get to work, right after I take care of this headache.[BLACK LETTERS]";
		now Intro is solved_noir;
		try fastforwarding instead.

	
[Noir Scenes]
NoirScene1 is a room with printed name "PI Office".
Understand "PI Office" as NoirScene1.
The description of NoirScene1 is "[one of][BLUE LETTERS]Ahhh, the old office. This place never lacked the charm that I so, truly  loved. It was cozy, dimly light and always had a slight, frigid bite that kept my nerves just the right amount on edge. The fan overhead cut through the dry air with every beat of my own black heart leaving shadows dancing around the room. It was time I got back to the business at hand, finding Celine. Last I remember, I had three vital pieces of information on her. Unfortunately, after coming onto my secretary last week, she hasn't been back to clean up my mess, women. Who knows where those files could be, better start looking.[BLACK LETTERS][or][BLUE LETTERS]I could lose myself in here for hours, but best not to dawdle - need to find those clues on Celine.[BLACK LETTERS][stopping]".

clues_number is a number that varies.

Carry out resetting in NoirScene1:
	now clues_number is 0;
	now top drawer is closed;
	now middle drawer is closed;
	now middle drawer is locked;
	now lower drawer is closed;
	now ball of paper is closed;
	now ball of paper is in trash can;
	now bronze key is in ball of paper;
	now Celine's Picture is in middle drawer;
	now Celine's Location is in NoirScene1;
	now Celine's Location is off-stage;
	now nearly finished bottle of whiskey is in lower drawer;
	now Celine's Secret is in NoirScene1;
	now Celine's Secret is off-stage;
	now empty bottle of whiskey is off-stage;
	now computer is not turned on;
	now computer is closed;
	
The bookshelf is a thing in NoirScene1. It is fixed in place.
The description is "[BLUE LETTERS]Not a single book on these shelves. I don't read much. So sue me.[BLACK LETTERS]".
The desk is a container in NoirScene1. It is transparent, openable, and fixed in place.
The description is "[BLUE LETTERS]The ol' desk, exotic oak. Was imported from Brazil after helping the President with a job. But that's ancient history, back when I was still new to the game - young and spritely. Glad that's over with.[BLACK LETTERS]".
The top drawer is a container in the desk. It is opaque, openable, closed and fixed in place.
The description is "[BLUE LETTERS]The top drawer, used to keep a gun in there - just in case. But now-a-days, with the way this city has been, that girl rarely leaves my side.[BLACK LETTERS]".
The middle drawer is a container in the desk. It is opaque, openable, closed, locked and fixed in place.
The description is "[BLUE LETTERS]I haven't the strangest idea why I still keep this locked. After I moved the whisky, I don't remember putting anything else in it. Then again, I'm always drinking to keep that darkness away...[BLACK LETTERS]".
The lower drawer is a container in the desk. It is opaque, openable, closed and fixed in place.
The description is "[BLUE LETTERS]Where I hide my liquor now, my secretary will never expect a thing. It's too obvious. Then again, secretaries now-a-days do require a college education. Maybe the young girl would get wise, if she'd ever come back.[BLACK LETTERS]".
The computer is a container in NoirScene1. The computer can be turned on. It is not turned on.
The computer is opaque, closed, unopenable, and fixed in place.
The description is "[BLUE LETTERS]My only link to the outside world now-a-days. Maybe it's my noir disposition, or maybe people are just all shit - but I don't get out much anymore. It's also the best place for me to get insider tips on the crime world. Just gotta give it a use to boot it up.[BLACK LETTERS]".
The trash can is a container in NoirScene1.  It is transparent and fixed in place.
The description is "[BLUE LETTERS]It's a trash can, where I generally throw away my crumpled-up failures.[BLACK LETTERS]".
The ball of paper is a container in the trash can. It is opaque , openable and closed.
The description is "[BLUE LETTERS]Seems like every other crumbled piece of paper scattered around this office.[BLACK LETTERS]".
The bronze key is a thing in the ball of paper.
The description is "[BLUE LETTERS]This was probably my secretaries' doing. She always knew I liked to keep my whiskey in that locked middle drawer. Shame she didn't know I caught on and moved it a while ago.[BLACK LETTERS]".
The Celine's Picture is in the middle drawer.
The description is "[BLUE LETTERS]She was beautiful, no - beautiful doesn't begin to describe a gal like Celine. Asphyxiating is more like it. So beautiful she'll rend the life out of every pore of your body if you even give her a milisecond. Looking at her face fills me with a morose, solemn darkness - but I guess that's nothing new for me.[BLACK LETTERS]".
The important email is a thing in the computer. It is fixed in place.
The description is "[BLUE LETTERS]The email was sent from an anonymous address. It only reads 'The Park, 12:00PM. xoxo' Better check it out.[BLACK LETTERS]".
The nearly finished bottle of whiskey is a thing in the lower drawer.
The description is "[BLUE LETTERS]What's it been, three days? Maybe I have a problem, but who cares. In this line of work, you're the problem if you don't have one. Speaking of, I could use a drink.[BLACK LETTERS]".
The Celine's Secret is a thing in NoirScene1.
The description is "[BLUE LETTERS]Without even having to think about it, I know exactly what this means. It can, after-all, mean only one thing - the way I'm going to get Celine.[BLACK LETTERS]".
The empty bottle of whiskey is a thing in the trash can.
The description is "[BLUE LETTERS]That trusty whiskey bottle served me well, if only for a few days. Hopefully it won't be the last.[BLACK LETTERS]".
The Celine's Location is a thing in NoirScene1.
The description is "[BLUE LETTERS]Information from that email I jotted down. 'The Park, 12:00PM. xoxo'. Could this be a message from Celine?[BLACK LETTERS]".

Check utilizing:
	if the noun is nearly finished bottle of whiskey:
		say "[BLUE LETTERS]A good drink is just what I need to get my head straight. I take a look at the small sum left, shrug, and kill it. The jarring humm in my mind that's nearly infinite is quieted down allowing me to think clearly. Just as I'm about to trash the bottle of whiskey, I spy writing on the inside of the label. Looks important, better take it.[BLACK LETTERS]";
		now Celine's Secret is on-stage;
		try taking Celine's Secret;
		now nearly finished bottle of whiskey is off-stage;
		now empty bottle of whiskey is on-stage;
		now empty bottle of whiskey is in the trash can;
		now clues_number is clues_number plus one instead;
	if the noun is computer and the computer is not turned on:
		say "[BLUE LETTERS]That obnoxious logo comes on with its irritating jingle to let me know the computer is booting up, how I hate that logo. Who cares honestly, just boot the damned thing up. What's this? An e-mail with the subject 'IMPORTANT'...[BLACK LETTERS]";
		now the computer is turned on;
		now the computer is open;
		now the important email is in the computer instead;
	if the noun is computer and the computer is turned on:
		say "[BLUE LETTERS]It's already turned on, honestly - how drunk am I?[BLACK LETTERS]" instead;
		
Check using:
	if the noun is bronze key and the second noun is middle drawer:
		say "[BLUE LETTERS]**CLICK**. I unlock the drawer with ease. Sliding it out, I can hear the rain beat down on the world outside, it feels like with every motion I take, it only grows stronger. The lightning flashes outside and my eyes re-focus on the picture of the beautiful Celene staring back at me.[BLACK LETTERS]";
		now the middle drawer is unlocked;
		now the middle drawer is open instead;
		
Carry out taking Celine's Picture:
		say "[BLUE LETTERS]It hurts to look at her face again, but taking this may come in handy. I'd never forget her face, but at least I can show it to witnesses.[BLACK LETTERS]";
		now clues_number is clues_number plus one.
		
Instead of taking important email:
	if the player does not have Celine's Location:
		say "[BLUE LETTERS]I better jot this down. Luckily it's still not midnight yet.[BLACK LETTERS]";
		now Celine's Location is on-stage;
		try taking Celine's Location;
		now clues_number is clues_number plus one instead;
	if the player does have Celine's Location:
		say "[BLUE LETTERS]I already took this note down. No reason to duplicate my efforts.[BLACK LETTERS]" instead.
		
Every turn rule while the player is in NoirScene1:
	if clues_number is 3:
		say "[BLUE LETTERS]The clues were all together, now it was just a matter of following them straight to Celine. I could feel the show-down inching forward. Will this act finally crescendo? Will I finally be able to put the end to all these years of hell...[BLACK LETTERS]";
		now NoirScene1 is solved_noir;
		try fastforwarding instead.
	

NoirScene2 is a room with printed name "The Park: Midnight".
Understand "The Park" and "Park" and "Midnight" as NoirScene2.
The description of NoirScene2 is "[one of][BLUE LETTERS]It's always sad when she gets the young ones. Staring at the most recent victim underneath this star-less nights, I can't help but look deeply into her cold, lifeless eyes  forcing me into that all-too-familiar trance. How many girls had it been, Celine? 20? 30? Will your bloodthirst never stop? This young beauty must have had the world open to her, so full of promise and you struck her down - for what? I could still smell Celine's infamous perfume in the air; she can't be far now - it's time to end this, if only I could find some clue to her whereabouts.[BLACK LETTERS][or][BLUE LETTERS]Sitting around here only reminds me of the horrible things Celine has done for all these years. This poor young girl's corpse stinks of the strange mixture of Celine's perfume and the absolute of death. Better find that lead and get out of here.[BLACK LETTERS][stopping]".

rom_item_value is a number that varies.
noir_item_value is a number that varies.

Carry out resetting in NoirScene2:
	now rom_item_value is 0;
	now noir_item_value is 0;

A thing can be droppable or undroppable.
A thing is usually droppable.
The kiss mark is a thing in NoirScene2. [ROMANCE]
The kiss mark is undroppable.
The description is "[BLUE LETTERS]I'd recognize those lips anywhere. And that shade of lipstick, well I still dream about it. Oh Celine, where do you go wrong... I just want to take that beautiful mark from this poor girl's cheek with me. If only rubbing away the mark would bring Celine back to me, maybe there wouldn't have had to be this waste of life.[BLACK LETTERS]".
The perfume is a thing in NoirScene2. [ROMANCE]
The perfume is undroppable.
The description is "[BLUE LETTERS]That perfume, just the smallest amount was enough to always keep my mind enraptured about Celine. I'll never forget that smell even after the day I die. I just want to take it with me; take Celine with me. I want to be with her forever, but I know it'll never be. Things have to be made right.[BLACK LETTERS]".
The note from Celine is a thing in NoirScene2. [ROMANCE]
The note from Celine is undroppable.
The description is "[BLUE LETTERS]'I know it's you, I can't wait to see you again. You know where to find me. xoxo Celine' So it was her that sent me that email. I need to set things right. I know I can save Celine. I won't make the same mistake I made back then. We'll be happy together and then I'll make ammends to it all. Just like it should be... Just like...[BLACK LETTERS]".
The dead girl's purse is a container in NoirScene2. It is opaque, openable, closed and fixed in place.
The description is "[BLUE LETTERS]The girl's purse, tattered and dirtied. It was probably once so pristine, but now soiled with a uncleanable dirt; much like her smile. This poor girl, no one will ever see that brilliant smile again.[BLACK LETTERS]".
The school ID is a thing in the dead girl's purse. [NOIR]
The school ID is undroppable.
The description is "[BLUE LETTERS]A Wilavert High School ID. The local high school here, filled with the best and the brightest this dark town still has. One of the last bastions of hope to re-light this barren wasteland and it just lost one of its brilliant wisps of light. Celine will pay for snuffing out such a hopeful mind.[BLACK LETTERS]".
The family photo is a thing in the dead girl's purse. [NOIR]
The family photo is undroppable.
The description is "[BLUE LETTERS]The family photo, every girl has to have one. It's painful to look at every time we come across one. There they all are, the girl, her brothers, her mother and father. They probably don't even know that their beloved, perfect daughter is now lying in the grass of a tall oak, battered, bruised and drained of every ounce of life she once had. Celine will know that feeling, I'll make sure of it.[BLACK LETTERS]".
The prom ticket is in the dead girl's purse. [NOIR]
The prom ticket is undroppable.
The description is "[BLUE LETTERS]'I can't wait for this night! I've been waiting so long to take you out!' I guess she had a boyfriend, or at least a date. The poor guy, he'll have to live with this news for the rest of his life. So excited to take out this girl and have a wonderful, memorable night together next weekend. He'll never heal from this wound, neither will the girl and most certainly - neither will Celine.[BLACK LETTERS]".

Instead of dropping something when the noun is undroppable:
	say "[BLUE LETTERS]No... This one is with me till the end.[BLACK LETTERS]" instead;

[ROMANCE ENDING ITEM RULES]
After examining kiss mark:
	if the player does not have the kiss mark:
		try taking kiss mark;
			
After examining perfume:
	if the player does not have the perfume:
		try taking perfume;
		
After examining note from Celine:
	if the player does not have the note from Celine:
		try taking note from Celine;

After taking kiss mark:		
	now rom_item_value is rom_item_value plus one instead;		
After taking perfume:
	now rom_item_value is rom_item_value plus one instead;
After taking note from Celine:
	now rom_item_value is rom_item_value plus one instead;

[NOIR ENDING ITEM RULES]
After examining school ID:
	if the player does not have the school ID:
		try taking school ID;

After examining family photo:
	if the player does not have the family photo:
		try taking family photo;
		now noir_item_value is noir_item_value plus one instead;

After examining prom ticket:
	if the player does not have the prom ticket:
		try taking prom ticket;
		now noir_item_value is noir_item_value plus one instead;
		
After taking school ID:
	now noir_item_value is noir_item_value plus one instead;
After taking family photo:
	now noir_item_value is noir_item_value plus one instead;
After taking prom ticket:
	now noir_item_value is noir_item_value plus one instead;	
	
Every turn rule while the player is in NoirScene2:
	if rom_item_value plus noir_item_value is three:
		if rom_item_value is greater than noir_item_value:
			now NoirScene2 is solved_romance;
			try fastforwarding instead;
		if noir_item_value is greater than rom_item_value:
			now NoirScene2 is solved_noir;
			try fastforwarding instead;
	

NoirScene3a is a room with printed name "Victim's House".
Understand "Victim's House" as NoirScene3a.
The description of NoirScene3a is "[BLUE LETTERS]I could feel it, on the otherside of the door was everything I worked for, it all culminated to one thing; Celine. You had finally tracked her down and it was time for the final showdown. Taking one last deep breath, you open the door and bathed in the stark, white light that flooded the room. [line break][line break]'The Jig is up, Celine.' you claimed, brandishing your trusty six-shooter towards Celene. [line break][line break]She laughs at your petty attempts to arrest her, 'Really? The jig is up? Please, do you honestly think I wasn't prepared for you? You follow someone for this long, you know they're going to notice.' You could see her own weapon pointed straight at that sweet spot between your eyes. Your eyes couldn't help but focus down the barrel, how many poor girls had seen this same scene? [line break][line break]'I see. So I guess we have ourselves a stand-off. So tell me, Celene - why did you do it? Why so many young ladies? What did they do wrong?' your questions cut through her like razor blades of ice and it was obvious.   [line break][line break]'What did they do WRONG!? You blithering idiot, you've chased my tail for ten years and you still haven't figured that much out?' her voice was raspt, wicked with hatred. [line break][line break]'Ten years is a long time Celene. Ever since you left my bed that morning and never came back - you changed. I don't even remember who you are anymore.' you said, each word filled with memories of passion, now tinted with disgust. [line break][line break]'It is a long time. You never got over me, did you? Still you lay up at night, longing for my kiss, my touch. I can see it in your hands even as you hold that pistol. You don't have the nerve to shoot me. You'll die a sad man, never knowing what happened to your wife or why she became the greatest serial killer this world had ever seen.' she finished, her thin lips now forming into a wicked smile. It was true, everything she said. You couldn't help but notice your right-hand, gripping the pistol so tightly it shaked with fear and cowardice. But before you even had time to react, a loud bang punctuated the night. It was all over. [line break][line break]You could hear her high-heels click and clack behind you as the door closed to not only end this chapter on your life, but the whole book.[BLACK LETTERS]".

NoirScene3b is a room with printed name "Motel in Cuba".
Understand "Celine's Hotel" as NoirScene3b.
The description of NoirScene3b is "'[BLUE LETTERS]I guess death wouldn't be so bad if it was started with a kiss from those wet lips of your's, Celine', I whispered in her frigid, deaf ears. So this is where it all came to an end. I guess you're probably wondering how we ended up here, in a dirty motel in the middle of god-knows-where Cuba. It's not a very interesting story and one comprised of all the puzzle pieces you probably already know. When I finally tracked down Celine, I couldn't pull the trigger. Something was holding me back. Maybe it was the darkness inside of her that I could see screaming at me behind her blue eyes, or maybe it was my own abyss beating for her's; but either way - I couldn't end it there. It just wasn't right. I needed her and she needed me. We tried, really we did. But we couldn't out-run all the horrible things she had done, all the beautiful motes of life she snuffed out demanded me to make it right. Part of me didn't want to do it, really it didn't - but it was as inevitable as the cyanide that now coarsed through her veins. Poor Celine, I really think she was the real victim in this whole scenario or maybe it's just how innocent she looks laying there, with that quiet, tranquil smile on her face as the winds of Cuba blow through the window over her pristine, motionless body. There was only one last thing I had to do to make everything right; hopefully in whatever eternal plane awaits us both, we can finally be rid of these insipid poisons that cursed us for so many years.[BLACK LETTERS]".


[Romance Scenes]
RomScene1 is room with printed name "Accounting Office".
Understand "Office" as RomScene1.
Understand "Accounting Office" as RomScene1.
The description of RomScene1 is 
	"[one of]
	[RED LETTERS]The Offices of Charles and Charles, Accounting LLC.  A hateful, miserly pair, long since dead, but their legacy had been carried forth by generations of successive skinflints bearing the Charles name until it came to be held by your present boss, a certain Mister Ulysses P. Charles the Third.  He keeps the workplace he manages dingy and unadorned in a bid to save on upkeep, and it is into one of the drab floors of this uninspiring business that you find yourself stumbling after your daring escape from the supply closet.[line break]You blink and rub your eyes — after the darkness of the cupboard, even this drab half-light stings your eyes.  As you adjust to the gloom,  you notice that there is no one else around.  Even your mysterious rescuer has disappeared.  This isn't so surprising — it is Boxing Day, and even Ulysses Charles would have a hard time getting his staff to come into work the day after Christmas.  Especially with the hangovers most of them would probably be exhibiting after the Christmas party last night…[line break]The Christmas party!  Celine!  It all comes flooding back again, all except where you were supposed to find her tonight.  Looking around the office some more, you realize that you're on the third floor, where human resources — and more importantly, Celine — works.  There must be some kind of clue to where she'll be at her desk, if only you can find it.[BLACK LETTERS]
	[or]
	[RED LETTERS]Your much-despised workplace.  Specifically, the offices of human resources, where Celine has her desk, which might have a clue as to where she'll be tonight.[BLACK LETTERS][stopping]".

Carry out resetting when the player is in RomScene1: 
	now Charles is in RomScene1;
	now genre_value is 1.

RomScene2 is room with printed name "The Park: Noon".
Understand "The Park" and "Park" and "Noon" as RomScene2.
The description of RomScene2 is "THIS IS A TEMP DESCRIPTION!!!".

RomScene3a is room with printed name "Celine's Bedroom".
Understand "Celine's Bedroom" as RomScene3a.
The ending_text of RomScene3a is "[one of]Oh my[stopping]".
The ending_turns of RomScene3a is 1.
RomScene3a is ending_room.
The description of RomScene3a is "THIS IS A TEMP DESCRIPTION!!!".

RomScene3b is room with printed name "Celine's Driveway".
Understand "Celine's Driveway" as RomScene3b.
RomScene3b is ending_room.
The ending_text of RomScene3b is "[one of]Oh my[stopping]".
The ending_turns of RomScene3b is 1.
The description of RomScene3b is "THIS IS A TEMP DESCRIPTION!!!".


Chapter V - Game Setup

The player is in Intro.

When play begins:
	now genre_value is 0;
	say "[BLUE LETTERS]I woke up in a haze, at that moment - memory wasn't exactly my strong suit. Quite a shame, when you're in the type of business I'm in, your memory is your only friend. My head was pounding, was I hit? Did someone ambush me? Where was I even? It was dark, that's for sure. The kind of jarring, confusing darkness that encrouches on your and sucks every bit of your breath like a void. A void, huh? Reminds me of my most recent case, Celine - a vicious, femme fatale known in twelve states for triple homicides. One can only imagine the void that lives inside that girl's heart, but that ain't my problem. My problem is bringing her whereabouts to light because that's who I am, a PI, the only light that can deshroud this dark, dank city. Speaking of light, about time I grab that trusty lighter of mine and figure out where I am.[BLACK LETTERS]";
	center "[line break]Press SPACE key to begin.";
	wait for the SPACE key;
	say "[line break][line break] [RED LETTERS]I'm not sure about that at all.  No, let's try that again.  How about this: you awake to find yourself in the dark — not in a figurative dark, of course, for that would be nothing out of the ordinary — but a real heavy, stifling dark, thick and treacly enough to cut with knife and serve with a lump of hot butter.  Your thoughts swim as your hands grope blindly.  You brain seems to be operating in low gear as you search for your last clear memory.  Blurred lights and swirling colors solidify in your mind into a face:  Celine!  That charming girl from human resources that you met at the office Christmas party.  You hit it off, she smiled that sweet smile, and she told you to meet her tonight at — where was it again?  And for that matter, where the blazes were you?";
	center "[line break][RED LETTERS]Alright, that sounds a bit better.  Carry on.[BLACK LETTERS]";
	center "[line break]Press SPACE key to begin."; 
	wait for the SPACE key;
	clear the screen;

Chapter VI - Action Defaults

[These defaults have to be at the end to avoid overriding other actions.]

Check utilizing:
	say "I can't use [the noun]." instead.

Check using: 
	say "I can't use those things like that." instead.
