"The Quarrel" by Zane Mariano & Jon Gill

Include Basic Screen Effects by Emily Short.
Include Reactable Quips by Michael Martin.
Release along with an interpreter, the source text and a website.
Use no scoring.

Chapter I - Values

[Data]
Genre_value is a number that varies.

Chapter II - Actions
	
[FASTFORWARD Action]	
Fastforwarding is an action applying to nothing.
Understand "do_fastforward" as fastforwarding.

move_to_room is a room that varies.
Instead of fastforwarding:
	let is_next_room be false;
	if location is solved_noir:
		let is_next_room be whether or not there is a next_room_noir corresponding to a curr_room of the location in the table of room connections;
		if is_next_room is true:
			now move_to_room is the next_room_noir corresponding to a curr_room of the location in the table of room connections;
		otherwise:
			say "[BLUE LETTERS]Nowhere to go.[BLACK LETTERS]";
	otherwise if location is solved_romance:
		let is_next_room be whether or not there is a next_room_romance corresponding to a curr_room of the location in the table of room connections;
		if is_next_room is true:
			now move_to_room is the next_room_romance corresponding to a curr_room of the location in the table of room connections;
		otherwise:
			say "[RED LETTERS]Drat!  There's nowhere for you to go.[BLACK LETTERS]";
	otherwise:
		say "[RED LETTERS]There's still work to be done here.[BLACK LETTERS]";
	if is_next_room is true:
		move the player to move_to_room, without printing a room description;
		try resetting;
		move the player to move_to_room;

Every turn when the location is not unsolved:
	try fastforwarding;
		

[REWIND Action]
Rewinding is an action applying to nothing.
Understand "rewind" as rewinding.

prev_room_trans is text that varies.
Instead of rewinding:
	let is_prev_room be false;
	let is_prev_room be whether or not there is a prev_room corresponding to a curr_room of the location in the table of room connections;
	if is_prev_room is true:
		say the description corresponding to a curr_room of the location in the table of rewind descriptions;
		[if the player consents:]
		now move_to_room is the prev_room corresponding to a curr_room of the location in the table of room connections;
		move the player to move_to_room, without printing a room description;
		try resetting;
		move the player to move_to_room;
	otherwise:
		if genre_value is 0:
			say "[BLUE LETTERS]There was no turning back now.[RED LETTERS] This is where you're meant to be.[BLACK LETTERS]";
		if genre_value is less than 0:
			say "[BLUE LETTERS]No... No turning back now. No more running.[BLACK LETTERS]";
		if genre_value is greater than 0:
			say "[RED LETTERS]This is where it all begins.  This is where you're meant to be.[BLACK LETTERS]";	

[RESET Action]
Resetting is an action applying to nothing.
Understand "test_reset" as resetting.
	
[Override RESET for each specific room.]


[Ending the game]
Ending_text_room is a room that varies.
After resetting when the location is ending_room:
	now ending_text_room is the location;
	the fat lady sings in the ending_turns of the location - 1 turns from now.

Every turn when the location is ending_text_room:
	say "[the ending_text of ending_text_room]";
	
At the time when the fat lady sings:
	end the game saying "THE END".
	

[--------------- Player Action Descriptions ---------------]


[UTILIZE]
[Utilize is the version of Use that applies to a single object]
Utilizing is an action applying to one visible thing.
Understand "utilize [something]" as utilizing.
Understand "use [something]" as utilizing.
Understand "turn on [something]" as utilizing.
Understand "write on [something]" as utilizing.

[USE]
[Use affects one object with another]
Using is an action applying to two visible things.
Understand "use [something] on [something]" as using.
Understand "use [something] with [something]" as using.
Understand "attack [something] with [something]" as using (with nouns reversed).
Understand "kick [something] with [something]" as using (with nouns reversed).
Understand "break [something] with [something]" as using (with nouns reversed).
Understand "apply [something] to [something]" as using.

[HELP]
Helping is an action applying to nothing. Understand "help" as helping.
After helping when genre_value is less than 0:
	say "[BLUE LETTERS] Time to EXAMINE myself.  Really TAKE a mental INVENTORY and have a deep LOOK inside.  I was a hard man, with hard convictions.  All I did was USE people for years.  Oh, well.  No way to REWIND time now.[BLACK LETTERS]";
After helping when genre_value is 0:
	say "[RED LETTERS]Maybe you do need to EXAMINE your priorities.[BLUE LETTERS] Really TAKE a mental INVENTORY and have a deep LOOK inside. [RED LETTERS] If only you could REWIND time, you think, but then you'd probably only make things worse, and what would be the USE of that?[BLACK LETTERS]";
After helping when genre_value is greater than 0:
	say "[RED LETTERS]Maybe you do need to EXAMINE your life.  TAKE a moment to get your thoughts together, see what's in your romantic INVENTORY.  If only you could REWIND time, you think, but then you'd probably only make things worse, and what would be the USE of that?[BLACK LETTERS]";


[EXAMINE ME]

Instead of examining player when genre_value is less than 0: say "Noir description.";
Instead of examining player when genre_value is 0: say "[RED LETTERS]Not especially young, and not especially handsome, but you'll do. [BLUE LETTERS] I get through each day with the thought of my next drink.[BLACK LETTERS]";
Instead of examining player when genre_value is greater than 0: say "[RED LETTERS]Not especially young, and not especially handsome, but you'll do.  You're passionate, about your loves at least, if not about your work.[BLACK LETTERS]";


[GO N/S/E/W]

Instead of going when genre_value is less than 0: say "[BLUE LETTERS]Yes, part of me wanted to run.  But this was where I had to be. [BLACK LETTERS]".
Instead of going when genre_value is 0: say "[RED LETTERS]Slow down, cowboy.  There's still [BLUE LETTERS]work to do.  Messy work.[BLACK LETTERS]".
Instead of going when genre_value is greater than 0: say "[RED LETTERS]Slow down, cowboy.  There's still something to do here before you go running off.[BLACK LETTERS]"

[Instead of doing something other than looking, examining, asking, telling, or going, say "HAHAHA NO."]

Chapter III - Conversations


[TALKING Action]

Talking is an action applying to one thing.
Understand "talk [something]" and "talk to [something]" as talking.
Check talking: try asking the noun about "the_default".
Check asking:
	if the text is "conversation_starter":
		continue the action;
	if the text is not "the_default":
		try asking the noun about "the_default".
Check telling:
	try asking the noun about "the_default".	


[--------------- Conversation Content -- Can be continued in other Chapters ---------------]


	Table of Ask Results (continued)
  	NPC  		topic  					result  
	Margaret		"conversation_starter"			hello there
	Margaret		"the_default"				hub

	Table of Tell Results (continued)
  	NPC  		topic			  		result  
  	Margaret  		"conversation_starter"		  	hub

	Table of Quip Texts (continued)
  	quip  		quiptext  
	hello there	"[RED LETTERS]'Oh, it's you!' gasps the girl breathlessly as she bounds up towards you.  'Fancy meeting you here!'  She eyes your rumpled clothes suspiciously.  'Have you not changed since the office party, you naughty boy?'  She does not have a lisp, but somehow she manages to deliver the last line as if she did.  You stammer a little as you try to compose a response.[BLACK LETTERS]"
	failed hello	"[RED LETTERS]You manage to mumble out some sort of greeting, but then your words fail.  The girl is looking at you with such excitement in her eyes that you really can't think of anything else to say.[BLACK LETTERS]"
	noir opening	""
	what did you call me	"[RED LETTERS]'Pardon, what did you call me?' the girl asks quizzically.  You gulp awkwardly and go back to examining your shoe laces.[BLACK LETTERS]"
	hub		"[RED LETTERS]The girl — [the printed name of Margaret] or whatever her name is — looks at you expectantly as you open your mouth.  The words almost seize up in your throat at that alone, but you manage to spit them out eventually.[BLACK LETTERS]"
	oh my		"[RED LETTERS]The girl covers her gasp with her hands, but it does little to hide the sly smile that spreads across her face.  'Well, you are forward, aren't you?' she mews.  'Do you really mean that?'[BLACK LETTERS]"
	noir continued	"[RED LETTERS]The girl puts her hand on her hip in what you assume she thinks is an alluring manner.  She makes a strange noise in the back of her throat, something that might have once been intended to be a growl but ended up as more of a strangled gargle.  'Hello, sailor,' she says.  'And how can I help you today?'[BLACK LETTERS]"
	why do you ask	"[RED LETTERS]The girl's eyes narrow.  'Might be I know where Celine lives.  Why do you ask?'[BLACK LETTERS]"
	i might know	"[RED LETTERS]She twirls a strand of her around her finger thoughtfully.  'And if I do tell you where Celine is?  What's in it for me?' she asks.[BLACK LETTERS]"
	disappointed	"[RED LETTERS]The girl's expression turns sour at that.  'Oh.'  She says sadly.  'I don't think I can help you there, then.'  The conversation dies with her smile, and you're left gazing awkwardly about the park again.[BLACK LETTERS]"
	noir success	"[BLUE LETTERS]The broad's smile grows so wide I wouldn't be surprised if it met round the back and half her head fell of.  'Alright, big boy,' she says, and I can barely keep from rolling my eyes.  'I'll take you to Celine's.  You'll owe me, though.  Maybe take me somewhere else after, hrm?'  I manage to hold back the gag, pulling a face that she takes for a grin.  Then she takes my hand and pulls be towards what I can only hope is Celine's.[BLACK LETTERS]"
	romance success	"[RED LETTERS]The girl pulls a face.  'I was wrong about you.  You have to be the dullest man alive,' she comments.  You'd be insulted, but you're not sure she's wrong.  'But if you'll do me a favor like that, I suppose I may as well help you out.  Come on, I'll show you where Celine lives.  God help her if she sees anything in you, though.'  She heads off down the path, and you follow feeling slightly diminished.[BLACK LETTERS]"
	

	Table of Quip Followups (continued)
  	quip  		option  								result  
  	hello there	 "[RED LETTERS]Oh, hello…[BLACK LETTERS]"				failed hello
  	hello there	 "[RED LETTERS]Fancy meeting you here…[BLACK LETTERS]"			failed hello
  	hello there	 "[RED LETTERS]Nice day, isn't it?[BLACK LETTERS]"				failed hello
  	hello there	 "[BLUE LETTERS]Well hey there…[BLACK LETTERS]"				noir opening
	noir opening	 "[BLUE LETTERS]…sweet cheeks.[BLACK LETTERS]"				noir continued
	noir opening	 "[BLUE LETTERS]… you saucy dame.[BLACK LETTERS]"			noir continued
	noir opening	 "[RED LETTERS]… [the printed name of Margaret].[BLACK LETTERS]"		what did you call me
	hub		"[RED LETTERS]Hello, [the printed name of Margaret].[BLACK LETTERS]"		what did you call me
	hub		"[RED LETTERS]Do you know Celine from work?[BLACK LETTERS]"		why do you ask
	hub		"[BLUE LETTERS]You. Me. Drinks. Now.[BLACK LETTERS]"			oh my
	oh my		"[BLUE LETTERS]You bet your sweet ass I do.[BLACK LETTERS]"		noir success
	oh my		"[RED LETTERS]Certainly. What else are friends for?[BLACK LETTERS]"		disappointed
	oh my		"[RED LETTERS]Well, Mr Charles gave me all this extra work to do, and I could use an extra brain to help me sort through it…[BLACK LETTERS]"	disappointed
	noir continued	"[RED LETTERS]I'm so sorry, that was terribly rude of me.[BLACK LETTERS]"	disappointed
	noir continued	"[BLUE LETTERS]Listen bright eyes, I'm looking for a dame.  Name of Celine.  Know her?[BLACK LETTERS]"	disappointed
	noir continued	"[RED LETTERS]I said, uh, [the printed name of Margaret].  Sorry, no, I meant [the printed name of Margaret].[BLACK LETTERS]"	what did you call me
	noir continued	"[BLUE LETTERS]You heard me.  Where can we go that's more private than the duck pond?[BLACK LETTERS]"	noir success
	why do you ask	"[RED LETTERS]She invited me to her party to night, but dash it if I can't remember her address.[BLACK LETTERS]"	I might know
	why do you ask	"[BLUE LETTERS]Girl's having a party tonight. Asked me along. Sounds like a dive, but that's how I like it.[BLACK LETTERS]"	I might know
	why do you ask	"[RED LETTERS]The truth of it is, I'm in love with her.[BLACK LETTERS]"		disappointed
	why do you ask	"[RED LETTERS]I was hoping you'd come with me to her party, [the printed name of Margaret].[BLACK LETTERS]"	what did you call me
	i might know	"[RED LETTERS]I'll do all your TP-14 forms for a month![BLACK LETTERS]"	romance success
	i might know	"[RED LETTERS]I'll put in a good word for you with Old Charles.[BLACK LETTERS]"	romance success
	i might know	"[BLUE LETTERS]Maybe you'll find out tonight.[BLACK LETTERS]"		noir success

	

[Quip Results]

Before quipping when the current quip is romance success:
	say "ROMANCE SUCCESS SOMEHOW";
	now RomScene2 is solved_romance;
Before quipping when the current quip is noir success:
	now RomScene2 is solved_noir;

Charles is a person.  The knife is an object.

Chapter IV - Rooms

[Room state]
A room can be unsolved, solved_noir, or solved_romance.  A room is usually unsolved.
Rooms have a text called ending_text.
Rooms have a number called ending_turns.
Rooms can be ending_room. A room is usually not ending_room.

[Room Relations]
	Table of Room Connections
	curr_room		next_room_noir	next_room_romance		prev_room
	Intro			NoirScene1		RomScene1				--
	NoirScene1		NoirScene2		--						Intro
	NoirScene2		NoirScene3a		NoirScene3b				NoirScene1
	RomScene1		--				RomScene2				Intro
	RomScene2		RomScene3a		RomScene3b				RomScene1				
	
	Table of Rewind Descriptions
	curr_room		description
	Intro			--
	NoirScene1		"[BLUE LETTERS] Like the Stygian Void, my memories are a whirlpool of uncertainty. [RED LETTERS] This story's getting darker than that closet.  Better head back and see if we can't change it.[BLACK LETTERS]"
	NoirScene2		"[BLUE LETTERS] I feel close to the end, but as my fear overwhelms me - I can't help but wonder if that was really how it all happened. [RED LETTERS]In fact, you know it wasn't.  Let's try it again, shall we?[BLACK LETTERS]"
	RomScene1		"[RED LETTERS]Wait, don't — [BLUE LETTERS]I shook the quaint, weirdly English daydream from my head.  Clearly I'd been hitting the pills too hard again.  Now, where was I?[BLACK LETTERS]"
	RomScene2		"[RED LETTERS]That's not quite how it happened, though, was it?  Let's try that last bit again…[BLACK LETTERS]"

[Intro Scene]
Intro is unsolved.

Intro is a room with printed name "The Closet: Trapped".
Understand "The Closet" and "Closet" as Intro.

The description of Intro is "[one of]
	[BLUE LETTERS]I find myself locked in a closet. You'd be surprised how often someone like me finds themselves in these sort of situations after dealing with gangsters and thugs too soft to land the killing blow. [RED LETTERS]That's a bit rich.  More often you find yourself in situations like this when Jenkins from third floor locks the door behind you while you're looking for a fresh pen.[BLUE LETTERS] They'd just as soon lock me away and hope the chemical fumes finish the job. Too bad for them, because escaping locked rooms was my speciality. [RED LETTERS] It certainly wasn't your work.  'Tardy, easily distracted, and poor with numbers,' your last performance review had read. [BLUE LETTERS]The closet was an archive closet where my secretary would store all the old case files from my brilliant triumphs and utter failures. [RED LETTERS]Actually, it smelled more like the janitorial supply closet.  Lovely. [BLUE LETTERS]Don't bother trying the door-knob though, it was [RED LETTERS]locked tight.[BLACK  LETTERS]
	[or][BLUE LETTERS]The closet is as dark and stifling as a coffin,[RED LETTERS] with a wet, vaguely antiseptic smell about it. [BLACK LETTERS][stopping]"

Carry out resetting in Intro:
	now Intro is unsolved;
	[----RomItems----]
	now the filing cabinet is closed;
	now the filing cabinet is not pushed;
	now the crumpled paper is off-stage;
	now the crumpled paper is not written on;
	now the basket is in Intro;
	now the mop is in Intro;
	[----NoirItems----]
	now Boots is in Intro;
	now Flimsy Doorknob is in Intro;
	now Gun Safe is in Intro;
	now Loaded Gun is in Gun Safe;
	
[---- Romance Solution for Intro ----]
The filing cabinet is a container in Intro. 
The filing cabinet is not portable.
The filing cabinet is closed and openable.
The filing cabinet can be pushed.  The filing cabinet is not pushed.
The basket is a container in Intro.
The description of the basket is "[RED LETTERS]Although it was intended to store pens, it has long-since been emptied by the sinister hand of petty corporate theft.[BLACK LETTERS]".
A crumpled paper is a thing.
The description of the crumpled paper is "A crumpled sheet of paper".
The paper can be written on.  The paper is not written on.
The dirty mop is a thing in Intro.
The description of the mop is "[RED LETTERS]The janitor's mop.  Ragged, splintery, and covered in some kind of inky black goop.[BLACK LETTERS]".
Goop is a part of the mop.  Understand "slime" and "goo" and "gunk" as goop.
The description of the goop is "[RED LETTERS]An disgusting, inky slime coating the fibers of the mop.[BLACK LETTERS]".
The closet door is an object in Intro. It is fixed in place.
The description of the closet door is "[RED LETTERS]Heavy, wooden, entirely unyielding.  Much like your boss when you asked for that raise. Light spills in through a tiny crack along the bottom of it.[BLACK LETTERS]".
The crack is part of the closet door.

Carry out examining filing cabinet:
	if filing cabinet is closed, say "[BLUE LETTERS]Closed, like the door to this closet.[RED LETTERS] You can make out the white corner of a scrap of paper sticking out from behind the cabinet, too small to grip with your fingers.[BLACK LETTERS]" instead;
	if filing cabinet is open, say "[BLUE LETTERS]Nothing in there but your hopes and dreams.  Which is to say, nothing in there.[BLACK LETTERS]". 
Instead of pulling filing cabinet:
	say "[RED LETTERS]Despite your thrice-weekly squash practices, you are unable to get a strong enough grip to pull the cabinet towards you.[BLACK LETTERS]" instead.
Instead of pushing filing cabinet:
	if the filing cabinet is not pushed:
		say "[RED LETTERS]You set your back against the cabinet, brace your legs against the wall, and give it the old college try.  You think you feel a blood vessel pop in your forehead, but you succeed in moving the cabinet a few inches.  As you regather your breath, you notice that a small piece of paper is sticking out from behind the cabinet.[BLACK LETTERS]";
		now the crumpled paper is in Intro;
		now the filing cabinet is pushed;
	otherwise:
		say "[RED LETTERS]You give the cabinet another push, wondering what else you might find behind it.  A tenner maybe?  Despite your hopes, though, the cabinet refuses to yield further ground.[BLACK LETTERS]".

Check examining crumpled paper when crumpled paper is off-stage:
	say "[RED LETTERS]A tiny white triangle of paper is visible, poking out from behind the filing cabinet.[BLACK LETTERS]" instead.

Carry out examining crumpled paper:
	if crumpled paper is not written on:
		say "[RED LETTERS]Somebody's typewritten notes.  Some rot about a case or a dame or something.  Had Jenkins been working on his novel during work hours again?[BLACK LETTERS]" instead;
	otherwise:
		say "[RED LETTERS]A ratty piece of paper with the word HELP scrawled across it in stinking mop goo.[BLACK LETTERS]" instead.

Check utilizing crumpled paper:
	if crumpled paper is not written on:
		say "[RED LETTERS]You need something to write on it with.  A pencil or ink, or at least something vaguely ink-like.[BLACK LETTERS]" instead;
	otherwise:
		say "[RED LETTERS]You've done as much as you can with this paper.  Now you just need somewhere to put it...[BLACK LETTERS]" instead.	

Carry out taking the mop:
	say "[RED LETTERS]You pick up the mop, gingerly avoiding the inky black goop that's worked into the head and oozing down the handle.[BLACK LETTERS]."
				
Carry out pushing the closet door: 
	say "[RED LETTERS]You press your shoulder against the wood and heave.  You achieve nothing more than a stiff back.[BLACK LETTERS]" instead.
Carry out opening the closet door: 
	say "[RED LETTERS]You give the handle a frustrated jiggle, but it is definitely locked.[BLACK LETTERS]" instead.
Carry out examining the crack: 
	say "[RED LETTERS]It's wide enough to slip something through, like a note or ribald birthday card.[BLACK LETTERS]" instead.

Check attacking closet door:
	say "[RED LETTERS]You would, but you don't want to bruise your hands.  That door looks hard.[BLACK LETTERS]" instead.
	
Check using:
	if the noun is mop:
		try utilizing the noun instead;
	if the noun is goop and the second noun is crumpled paper:
		now crumpled paper is written on;
		say "[RED LETTERS]You suppress a shudder as you rub the tarry substance over the paper.  When you are done your hands are stained and the word HELP is scrawled across the sheet, crude and malodrous.[BLACK LETTERS]" instead;
	if the noun is crumpled paper and the second noun is crack:
		if crumpled paper is not written on:
			say "[RED LETTERS]You begin to slide the paper under the door, but realize that sharing someone else's notes with the wider world will not do much to aid you in your current predicament.  You withdraw the paper from the crack.[BLACK LETTERS]" instead;
		otherwise:
			say "[RED LETTERS]You slide your desperate missive through the crack beneath the door.  After a few minutes, you hear someone walk towards the door, pick up the note, and pause.  A few seconds later, the latch clicks, the door opens, and you are blinded by the bright light that spills into the closet from outside.[BLACK LETTERS]";
			now Intro is solved_romance;
			try fastforwarding instead;
	if the noun is crumpled paper and crumpled paper is not written on:
		try utilizing crumpled paper instead.

Check utilizing:
	if the noun is mop:
		say "[RED LETTERS]There's nothing to clean.  Besides, that goop on the end of it would probably leave more of a mess than was already there.[BLACK LETTERS]" instead;
	if the noun is crumpled paper:
		say "[BLUE LETTERS]Just ancient history now.[BLACK LETTERS]".

[ ----- Noir Solution for Intro ----- ]
	
The pair of boots are a thing in Intro.  It is wearable.  The description is "[BLUE LETTERS]So this is where I left my trusty pair of boots.  [RED LETTERS] Sort of a strange place, but you suppose they could come in handy.[BLUE LETTERS] Just looking at them reminds me of the good times we had together, kicking down doors to gangster's houses and the such. [BLACK LETTERS]".
The flimsy doorknob is a thing in Intro. The description is "[BLUE LETTERS][RED LETTERS]Where you work,[BLUE LETTERS] you don't really have the kind of money to fix doorknobs.  This one has always been especially flimsy.[BLACK LETTERS]".
The flimsy doorknob is fixed in place.
The gun safe is a container in Intro. The description is "[BLUE LETTERS]Ahhh, the gun safe. I never kept it locked because honestly, if someone broke into my office - I deserve to be shot. [RED LETTERS] Why would you say that?  What kind of horrible story is this? [BLACK LETTERS] Better to keep it unlocked and grab that loaded revolver in times of emergency.[BLACK LETTERS]".
The gun safe is openable, closed, opaque and fixed in place.
The loaded gun is a thing in the gun safe. The description is "[BLUE LETTERS]The cold steel of the revolver is something all too familiar to my palm. [RED LETTERS] No, it isn't.  Your hand trembles as you pick up the unfamiliar lump of metal, so much so that you almost drop it on the floor.[BLACK LETTERS]".

After taking the pair of boots:
	say "[RED LETTERS]I hope you're not planning on kicking anything with those!  I shan't abide that kind of violence in this story.";
	try wearing the boots instead;
	
Check attacking the flimsy doorknob:
	say "[RED LETTERS]How dreadful!  [BLUE LETTERS]I didn't know what to smash it with.[BLACK LETTERS]" instead.

Check using: 
	if the noun is pair of boots and the second noun is the flimsy doorknob:
		say "[BLUE LETTERS]Strapping the sturdy boots on my feet, I feel revitalized. With a quick heave, that flimsy doorknob shatters straight off the door. That's about enough excitment for one day, best to strap down with a victory whiskey in my office. But I guess there's still Celine to find...[BLACK LETTERS]";
		now Intro is solved_noir;
		try fastforwarding instead;
	if the noun is the loaded gun and the second noun is the flimsy doorknob:
		say "[BLUE LETTERS]I look at the gun in my hand, and then at the doorknob. It seems like a simple solution, but probably not the greatest. I'm gonna need a big glass of whiskey to get rid of the banging headache that this is sure to give me. But fuck it, I've got to get out of there somehow... [line break][line break]*BANG*[line break][line break] The revolver explodes with a bright light, smoke, and a painful ringing in my ears. Obviously, there's no evidence left of the doorknob's once residence on the door. Time to get to work, right after I take care of this headache.[BLACK LETTERS]";
		now Intro is solved_noir;
		try fastforwarding instead.

	
[Noir Scenes]
NoirScene1 is a room with printed name "PI Office".
Understand "PI Office" as NoirScene1.
The description of NoirScene1 is "[one of][BLUE LETTERS]Ahhh, the old office. This place never lacked the charm that I so, truly loved. It was [RED LETTERS]cozy,[BLUE LETTERS] dimly lit and always had a slight, frigid bite that kept my nerves just the right amount on edge. The fan overhead cut through the dry air with every beat of my own black heart leaving shadows dancing around the room. It was time I got back to the business at hand, finding Celine. [RED LETTERS]Now we're talking.  Forget all the violence and the grunge and get back to finding the girl. [BLACK LETTERS] Last I remember, I had three vital pieces of information on her. Unfortunately, after coming onto my secretary last week, she hasn't been back to clean up my mess… women. Who knows where those files could be, better start looking. [RED LETTERS] Or you could turn around right now and REWIND back to before things went so bloody Chinatown all over the place.[BLACK LETTERS][or][BLUE LETTERS]I could lose myself in here for hours, but best not to dawdle - need to find those clues on Celine [RED LETTERS] or REWIND back to someplace a bit cheerier, like the inside of locked closet.[BLACK LETTERS][stopping]".

clues_number is a number that varies.

Carry out resetting in NoirScene1:
	now clues_number is 0;
	now top drawer is closed;
	now middle drawer is closed;
	now middle drawer is locked;
	now lower drawer is closed;
	now ball of paper is closed;
	now ball of paper is in trash can;
	now bronze key is in ball of paper;
	now Celine's Picture is in middle drawer;
	now Celine's Location is in NoirScene1;
	now Celine's Location is off-stage;
	now nearly finished bottle of whiskey is in lower drawer;
	now Celine's Secret is in NoirScene1;
	now Celine's Secret is off-stage;
	now empty bottle of whiskey is off-stage;
	now computer is not turned on;
	now computer is closed;
	
The bookshelf is a thing in NoirScene1. It is fixed in place.
The description is "[BLUE LETTERS]Not a single book on these shelves. I don't read much. So sue me.[BLACK LETTERS]".
The desk is a container in NoirScene1. It is transparent, openable, and fixed in place.
The description is "[BLUE LETTERS]The ol' desk, exotic oak. Was imported from Brazil after helping the President with a job. But that's ancient history, back when I was still new to the game - young and spritely. Glad that's over with.[BLACK LETTERS]".
The top drawer is a container in the desk. It is opaque, openable, closed and fixed in place.
The description is "[BLUE LETTERS]The top drawer, used to keep a gun in there - just in case. But now-a-days, with the way this city has been, that girl rarely leaves my side. [RED LETTERS] What is going on with all the bloody guns in this story?[BLACK LETTERS]".
The middle drawer is a container in the desk. It is opaque, openable, closed, locked and fixed in place.
The description is "[BLUE LETTERS]I haven't the strongest idea why I still keep this locked. After I moved the whisky, I don't remember putting anything else in it. Then again, I'm always drinking to keep that darkness away... [RED LETTERS] You're a tortured soul now, are you?  Nice.[BLACK LETTERS]".
The lower drawer is a container in the desk. It is opaque, openable, closed and fixed in place.
The description is "[BLUE LETTERS]Where I hide my liquor now, my secretary will never expect a thing. It's too obvious. Then again, secretaries now-a-days do require a college education. Maybe the young girl would get wise, if she'd ever come back.  [RED LETTERS]To a place like this?  Hardly.[BLACK LETTERS]".
The computer is a container in NoirScene1. The computer can be turned on. It is not turned on.
The computer is opaque, closed, unopenable, and fixed in place.
The description is "[BLUE LETTERS]My only link to the outside world now-a-days. Maybe it's my disposition, or maybe people are just all shit - but I don't get out much anymore. [RED LETTERS]Clearly.[BLACK LETTERS] It's also the best place for me to get insider tips on the crime world. Just gotta give it a use to boot it up.[BLACK LETTERS]".
The trash can is a container in NoirScene1.  It is transparent and fixed in place.
The description is "[BLUE LETTERS]It's a trash can, where I generally throw away my crumpled-up failures.  [RED LETTERS]Like your narration.  Come on, let's REWIND this back to when I was in charge.[BLACK LETTERS]".
The ball of paper is a container in the trash can. It is opaque , openable and closed.
The description is "[BLUE LETTERS]Seems like every other crumbled piece of paper scattered around this office.[BLACK LETTERS]".
The bronze key is a thing in the ball of paper.
The description is "[BLUE LETTERS]This was probably my secretary's doing. She always knew I liked to keep my whiskey in that locked middle drawer. Shame she didn't know I caught on and moved it a while ago.[BLACK LETTERS]".
The Celine's Picture is in the middle drawer.
The description is "[BLUE LETTERS]She was beautiful, no - beautiful [RED LETTERS]doesn't begin to describe a girl like Celine.[BLACK LETTERS] Asphyxiating is more like it. [RED LETTERS]Charming.[BLACK LETTERS] So beautiful she'll rend the life out of every pore of your body if you even give her a milisecond. Looking at her face fills me with a morose, solemn darkness - but I guess that's nothing new for me.[BLACK LETTERS]".
The important email is a thing in the computer. It is fixed in place.
The description is "[BLUE LETTERS]The email was sent from an anonymous address. It only reads 'The Park, 12:00PM. xoxo' Better check it out. [RED LETTERS] You always were a sucker for hugs and kisses.[BLACK LETTERS]".
The nearly finished bottle of whiskey is a thing in the lower drawer.
The description is "[BLUE LETTERS]What's it been, three days? Maybe I have a problem, but who cares. In this line of work, you're the problem if you don't have one. Speaking of, I could use a drink.[BLACK LETTERS]".
The Celine's Secret is a thing in NoirScene1.
The description is "[BLUE LETTERS]Without even having to think about it, I know exactly what this means. It can, after-all, mean only one thing - the way I'm going to get Celine.[BLACK LETTERS]".
The empty bottle of whiskey is a thing in the trash can.
The description is "[BLUE LETTERS]That trusty whiskey bottle served me well, if only for a few days. Hopefully it won't be the last.[BLACK LETTERS]".
The Celine's Location is a thing in NoirScene1.
The description is "[BLUE LETTERS]Information from that email I jotted down. 'The Park, 12:00PM. xoxo'. Could this be a message from Celine?[BLACK LETTERS]".

Check utilizing:
	if the noun is nearly finished bottle of whiskey:
		say "[BLUE LETTERS]A good drink is just what I need to get my head straight. I take a look at the small sum left, shrug, and kill it. [RED LETTERS]Small amount left, you mean.  A sum is only for a numerically distinct group.  Nincompoop.[BLUE LETTERS] The jarring, unending hum in my mind is quieted down allowing me to think clearly. Just as I'm about to trash the bottle of whiskey, I spy writing on the inside of the label. Looks important, better take it.[BLACK LETTERS]";
		now Celine's Secret is on-stage;
		try taking Celine's Secret;
		now nearly finished bottle of whiskey is off-stage;
		now empty bottle of whiskey is on-stage;
		now empty bottle of whiskey is in the trash can;
		now clues_number is clues_number plus one instead;
	if the noun is computer and the computer is not turned on:
		say "[BLUE LETTERS]That obnoxious logo comes on with its irritating jingle to let me know the computer is booting up, how I hate that logo. Who cares honestly, just boot the damned thing up. What's this? An e-mail with the subject 'IMPORTANT'...[BLACK LETTERS]";
		now the computer is turned on;
		now the computer is open;
		now the important email is in the computer instead;
	if the noun is computer and the computer is turned on:
		say "[BLUE LETTERS]It's already turned on, honestly - how drunk am I?[BLACK LETTERS]" instead;
		
Check drinking the nearly finished bottle of whiskey:
	try utilizing the nearly finished bottle of whiskey instead;

Check using:
	if the noun is bronze key and the second noun is middle drawer:
		say "[BLUE LETTERS]**CLICK**. I unlock the drawer with ease. Sliding it out, I can hear the rain beat down on the world outside, it feels like with every motion I take, it only grows stronger. The lightning flashes outside and my eyes re-focus on the picture of the beautiful Celine staring back at me.[BLACK LETTERS]";
		now the middle drawer is unlocked;
		now the middle drawer is open instead;
		
Carry out taking Celine's Picture:
		say "[BLUE LETTERS]It hurts to look at her face again, but taking this may come in handy. I'd never forget her face, but at least I can show it to witnesses.  [RED LETTERS]Witnesses?  Good God, what sort of love story is this?[BLACK LETTERS]";
		now clues_number is clues_number plus one.
		
Instead of taking important email:
	if the player does not have Celine's Location:
		say "[BLUE LETTERS]I better jot this down. Luckily it's still not midnight yet.[BLACK LETTERS]";
		now Celine's Location is on-stage;
		try taking Celine's Location;
		now clues_number is clues_number plus one instead;
	if the player does have Celine's Location:
		say "[BLUE LETTERS]I already took this note down. No reason to duplicate my efforts.[BLACK LETTERS]" instead.
		
Every turn rule while the player is in NoirScene1:
	if clues_number is 3:
		say "[BLUE LETTERS]The clues were all together, now it was just a matter of following them straight to Celine. I could feel the showdown inching forward. Will this act finally crescendo? Will I finally be able to put the end to all these years of hell... [RED LETTERS]Will this dirge of a story finally end?  We can only hope.[BLACK LETTERS]";
		now NoirScene1 is solved_noir;
		try fastforwarding instead.
	

NoirScene2 is a room with printed name "The Park: Midnight".
Understand "The Park" and "Park" and "Midnight" as NoirScene2.
The description of NoirScene2 is "[one of][BLUE LETTERS]It's always sad when she gets the young ones. Staring at the most recent victim underneath this star-less nights, I can't help but look deeply into her [RED LETTERS]pretty,[BLUE LETTERS] lifeless eyes, forcing me into that all-too-familiar trance. How many girls had it been, Celine? 20? 30? Will your bloodthirst never stop? This young beauty must have had the world open to her, so full of promise and you struck her down - for what? I could still smell Celine's infamous perfume in the air; she can't be far now.  It's time to end this, if only I could find some clue to her whereabouts.  [RED LETTERS]Then maybe you can tell her how you really feel…[BLACK LETTERS][or][BLUE LETTERS]Sitting around here only reminds me of the horrible things Celine has done for all these years. This poor young girl's corpse stinks of the strange mixture of Celine's perfume and the absolute of death. Better find that lead and get out of here.[BLACK LETTERS][stopping]".

rom_item_value is a number that varies.
noir_item_value is a number that varies.

Carry out resetting in NoirScene2:
	now rom_item_value is 0;
	now noir_item_value is 0;

A thing can be droppable or undroppable.
A thing is usually droppable.
The kiss mark is a thing in NoirScene2. [ROMANCE]
The kiss mark is undroppable.
The description is "[RED LETTERS]You'd recognize those lips anywhere. [BLUE LETTERS]And that shade of lipstick, well I still dream about it.  [RED LETTERS]Where did we go wrong, Celine?[BLUE LETTERS] I just want to take that beautiful mark from this poor girl's cheek with me. If only rubbing away the mark would bring Celine back to me, maybe there wouldn't have had to be this waste of life.[BLACK LETTERS]".
The perfume is a thing in NoirScene2. [ROMANCE]
The perfume is undroppable.
The description is "[BLUE LETTERS]That perfume, just the smallest amount was enough to always keep my mind enraptured about Celine. I'll never forget that smell even after the day I die. [RED LETTERS]You just want to take it with you; take Celine with you.[BLUE LETTERS] I want to be with her forever, but I know it'll never be. Things have to be made right.[BLACK LETTERS]".
The note from Celine is a thing in NoirScene2. [ROMANCE]
The note from Celine is undroppable.
The description is "[BLUE LETTERS]'I know it's you, I can't wait to see you again. You know where to find me. xoxo Celine' So it was her that sent me that email. I need to set things right. [RED LETTERS]You know you can save Celine.  You can still get this love story gone wrong back on track.[BLUE LETTERS] I won't make the same mistake I made back then. We'll be happy together and then I'll make amends for it all. [RED LETTERS]Just like it should be.[BLACK LETTERS]".
The dead girl's purse is a container in NoirScene2. It is opaque, openable, closed and fixed in place.
The description is "[BLUE LETTERS]The girl's purse, tattered and dirtied. It was probably once so pristine, but now soiled with a uncleanable dirt; much like her smile. This poor girl, no one will ever see that brilliant smile again.[BLACK LETTERS]".
The school ID is a thing in the dead girl's purse. [NOIR]
The school ID is undroppable.
The description is "[BLUE LETTERS]A Wilavert High School ID. The local high school here, filled with the best and the brightest this dark town still has. One of the last bastions of hope to re-light this barren wasteland and it just lost one of its brilliant wisps of light. Celine will pay for snuffing out such a hopeful mind.[BLACK LETTERS]".
The family photo is a thing in the dead girl's purse. [NOIR]
The family photo is undroppable.
The description is "[BLUE LETTERS]The family photo, every girl has to have one. It's painful to look at every time we come across one. There they all are, the girl, her brothers, her mother and father. They probably don't even know that their beloved, perfect daughter is now lying in the grass of a tall oak, battered, bruised and drained of every ounce of life she once had. Celine will know that feeling, I'll make sure of it.[BLACK LETTERS]".
The prom ticket is in the dead girl's purse. [NOIR]
The prom ticket is undroppable.
The description is "[BLUE LETTERS]'I can't wait for this night! I've been waiting so long to take you out!' I guess she had a boyfriend, or at least a date. The poor guy, he'll have to live with this news for the rest of his life. So excited to take out this girl and have a wonderful, memorable night together next weekend. He'll never heal from this wound, neither will the girl and most certainly - neither will Celine.[BLACK LETTERS]".

Instead of dropping something when the noun is undroppable:
	say "[BLUE LETTERS]No... This one is with me till the end.[BLACK LETTERS]" instead;

[ROMANCE ENDING ITEM RULES]
After examining kiss mark:
	if the player does not have the kiss mark:
		try taking kiss mark;
			
After examining perfume:
	if the player does not have the perfume:
		try taking perfume;
		
After examining note from Celine:
	if the player does not have the note from Celine:
		try taking note from Celine;

After taking kiss mark:		
	now rom_item_value is rom_item_value plus one instead;		
After taking perfume:
	now rom_item_value is rom_item_value plus one instead;
After taking note from Celine:
	now rom_item_value is rom_item_value plus one instead;

[NOIR ENDING ITEM RULES]
After examining school ID:
	if the player does not have the school ID:
		try taking school ID;

After examining family photo:
	if the player does not have the family photo:
		try taking family photo;
		now noir_item_value is noir_item_value plus one instead;

After examining prom ticket:
	if the player does not have the prom ticket:
		try taking prom ticket;
		now noir_item_value is noir_item_value plus one instead;
		
After taking school ID:
	now noir_item_value is noir_item_value plus one instead;
After taking family photo:
	now noir_item_value is noir_item_value plus one instead;
After taking prom ticket:
	now noir_item_value is noir_item_value plus one instead;	
	
Every turn rule while the player is in NoirScene2:
	if rom_item_value plus noir_item_value is three:
		if rom_item_value is greater than noir_item_value:
			now NoirScene2 is solved_romance;
			try fastforwarding instead;
		if noir_item_value is greater than rom_item_value:
			now NoirScene2 is solved_noir;
			try fastforwarding instead;
	

NoirScene3a is a room with printed name "Victim's House".
Understand "Victim's House" as NoirScene3a.
The description of NoirScene3a is "[BLUE LETTERS]I could feel it, on the otherside of the door was everything I worked for, it all culminated to one thing; Celine. I had finally tracked her down and it was time for the final showdown. [RED LETTERS]Too late to go back now.  You made you choice as to what kind of story this would be.[BLUE LETTERS] Taking one last deep breath, I open the door and become bathed in the stark, white light that flooded the room. [line break][line break]'The Jig is up, Celine.' I claimed, brandishing my trusty six-shooter towards Celene. [line break][line break]She laughs at my petty attempts to arrest her, 'Really? The jig is up? Please, do you honestly think I wasn't prepared for you? You follow someone for this long, you know they're going to notice.' I could see her own weapon pointed straight at that sweet spot between my eyes. My eyes couldn't help but focus down the barrel, how many poor girls had seen this same scene? [line break][line break]'I see. So I guess we have ourselves a stand-off. So tell me, Celine - why did you do it? Why so many young ladies? What did they do wrong?' My questions cut through her like razor blades of ice and it was obvious.   [line break][line break]'What did they do WRONG!? You blithering idiot, you've chased my tail for ten years and you still haven't figured that much out?' her voice was rasped and wicked with hatred. [line break][line break]'Ten years is a long time, Celine. Ever since you left my bed that morning and never came back - you changed. I don't even remember who you are anymore.' I said, [RED LETTERS]each word filled with memories of passion,[BLUE LETTERS] now tinted with disgust.[line break][line break]'It is a long time. You never got over me, did you? Still you lay up at night, longing for my kiss, my touch. I can see it in your hands even as you hold that pistol. You don't have the nerve to shoot me. You'll die a sad man, never knowing what happened to your wife or why she became the greatest serial killer this world had ever seen,' she finished, her thin lips now forming into that wicked smile. It was true, everything she said. I couldn't help but notice my right-hand, gripping the pistol so tightly it shook with fear and her words. But before I even had time to react, a loud bang punctuated the night. It was all over.[line break][line break]I could hear her high-heels click and clack behind me as the door closed to not only end this chapter on my life, but the whole book.[BLACK LETTERS]".

NoirScene3b is a room with printed name "Motel in Cuba".
Understand "Motel" as NoirScene3b.
The description of NoirScene3b is "'[RED LETTERS]I guess death wouldn't be so bad if it was started with a kiss from those wet lips of yours, Celine', [BLUE LETTERS]I whispered in her frigid, deaf ears. So this is where it all came to an end. I guess you're probably wondering how we ended up here, in a dirty motel in the middle of god-knows-where Cuba. It's not a very interesting story and one comprised of all the puzzle pieces you probably already know. [RED LETTERS]When you finally tracked down Celine, you couldn't pull the trigger.[BLUE LETTERS] Something was holding me back. Maybe it was the darkness inside of her that I could see screaming at me behind her blue eyes, or maybe it was my own abyss beating for hers, but either way - I couldn't end it there. It just wasn't right.[RED LETTERS]You needed her and she needed you.[BLUE LETTERS] We tried, really we did. But we couldn't out-run all the horrible things she had done, all the beautiful motes of life she snuffed out demanded me to make it right. Part of me didn't want to do it, really it didn't - but it was as inevitable as the cyanide that now coarsed through her veins. Poor Celine, I really think she was the real victim in this whole scenario or maybe it's just how innocent she looks laying there, with that quiet, tranquil smile on her face as the winds of Cuba blow through the window over her pristine, motionless body. There was only one last thing I had to do to make everything right; hopefully in whatever eternal plane awaits us both, we can finally be rid of these insipid poisons that cursed us for so many years.[BLACK LETTERS]".


[Romance Scenes]
RomScene1 is room with printed name "Accounting Office".
Understand "Office" as RomScene1.
Understand "Accounting Office" as RomScene1.
The description of RomScene1 is 
	"[one of]
	[RED LETTERS]The Offices of Charles and Charles, Accounting LLC.  A hateful, miserly pair, long since dead, but their legacy had been carried forth by generations of successive skinflints bearing the Charles name until it came to be held by your present boss, a certain Mister Ulysses P. Charles the Third.  He keeps the workplace he manages dingy and unadorned in a bid to save on upkeep, and it is into one of the drab floors of this uninspiring business that you find yourself stumbling after your daring escape from the supply closet.[line break]You blink and rub your eyes — after the darkness of the cupboard, even this drab half-light stings your eyes.  As you adjust to the gloom,  you notice that there is no one else around.  Even your mysterious rescuer has disappeared.  This isn't so surprising — it is Boxing Day, and even Ulysses Charles would have a hard time getting his staff to come into work the day after Christmas.  Especially with the hangovers most of them would probably be exhibiting after the Christmas party last night…[line break]The Christmas party!  Celine!  It all comes flooding back again, all except where you were supposed to find her tonight.  Looking around the office some more, you realize that you're on the third floor, where human resources — and more importantly, Celine — works.  There must be some kind of clue to where she'll be at her desk, if only you can find it.[BLACK LETTERS]
	[or]
	[RED LETTERS]Your much-despised workplace.  Specifically, the offices of human resources, where Celine has her desk, which might have a clue as to where she'll be tonight.[BLACK LETTERS][stopping]".

Carry out resetting when the player is in RomScene1: 
	now the pink flower is on the light brown desk;
	now the pink day planner is on the light brown desk;
	now the blue flower is on the moderately brown desk;
	now the blue day planner is on the moderately brown desk;
	now the pint glass is on the dark brown desk;
	now the grey day planner is on the dark brown desk;
	now your crumpled jacket is in the cardboard box;
	now the cardboard box is closed;
	now the scribbled note is off-stage;
	now RomScene1 is unsolved;
	now genre_value is 1.
	
A planner is a kind of thing.  A planner can be openable.  A planner is openable.  A planner can be open or closed.  A planner is usually closed.

The light brown desk is a supporter in RomScene1.
The description of the light brown desk is "[RED LETTERS]One of many such wooden desks that fill the floors of Charles & Charles.  This one is a light, beigeish color.[BLACK LETTERS]".

The pink flower is a thing on the light brown desk.
The description is "[RED LETTERS]A pink carnation in a round glass bowl.  Charming.[BLACK LETTERS]".

The pink day planner is a planner on the light brown desk.

The moderately brown desk is a supporter in RomScene1.
The description of the moderately brown desk is "[RED LETTERS]One of the hated wooden desks that define the workplace experience for your colleagues and yourself.  This one has a middling, uninspired brown hue to it.[BLACK LETTERS]".

The blue flower is a thing on the moderately brown desk.
The description is "[RED LETTERS]A blue blossom in a hand-blown black bowl.  Blissful.[BLACK LETTERS]".

The old computer is a thing on the moderately brown desk.
The description is "[RED LETTERS]An IBM model almost as old as you are.  Its screen is dark, dusty, and flecked with the greyish residue of long-dry spittle.  Lovely.[BLACK LETTERS]".

The blue day planner is a planner on the moderately brown desk.

The dark brown desk is a supporter in RomScene1.  It is fixed in place.
The description of the dark brown desk is "[RED LETTERS]The darkest of the desks in the room, although it's less a deep oaken hue and more of an unpleasant oily patina.[BLACK LETTERS]".

The pint glass is a thing on the dark brown desk.
The description is "[RED LETTERS]A dirty pint glass, it's sides webbed with dry foam.[BLACK LETTERS]".

The grey day planner is a planner on the dark brown desk.

The cardboard box is a container in RomScene1.  The cardboard box is fixed in place, openable, opaque, and closed.
The description is "[RED LETTERS]A white cardboard box usually used to store paper and other such businessy nonsense.[BLACK LETTERS]".

Your crumpled jacket is a thing in the cardboard box.  The crumpled jacket is wearable.
The description is "[RED LETTERS]Your own crumpled suit jacket.  You must have thrown it aside at some point during the previous night.[BLACK LETTERS]".
The pockets are a part of your crumpled jacket.  Understand "pocket" as pockets.

The supply closet door is a thing in RomScene1.
The description is "[RED LETTERS]The portal back to that bleak netherealm in which you spent the night.  No thank you, sir.[BLACK LETTERS]".

The scribbled note is a thing.  The scribbled note can be read.  The scribbled note is not read.
The description is "[RED LETTERS]A crumpled piece of paper in your jacket pocket.  Scrawled upon it in red ballpoint is 'CELINE M. 525-423-9820'.  You always did have a way with women while in your cups.  You card, you.[BLACK LETTERS]."

Check examining pockets when the player is wearing your crumpled jacket:
	now the scribbled note is read;
	say "[RED LETTERS]You can feel a crumpled piece of paper in your jacket pocket.[BLACK LETTERS]" instead.

Carry out examining the pink day planner:
	if the noun is closed:
		say "[RED LETTERS]The cover reads [line break]Official Employee Planner for Business and Personal Life Events[line break]Property of Charles & Charles Accounting LLC.[line break]Employee Details:  5261 Magnolia Court — 525-180-7547'[line break]  You weren't allowed to write your names on your planners here — Mr Charles said it encouraged his staff to have too much personality.[BLACK LETTERS]";
	if the noun is open:
		say "[RED LETTERS]You thumb through to the entry for December 26.  It seems the owner of this planner is going to be at the roller rink tonight.  That could be fun, you think, but could it be Celine?[BLACK LETTERS]";

Carry out examining the blue day planner:
	if the noun is closed:
		say "[RED LETTERS]The cover reads [line break]Official Employee Planner for Business and Personal Life Events[line break]Property of Charles & Charles Accounting LLC.[line break]Employee Details:   925 Tulip Way — 525-282-9242'[line break]  Charles liked to keep his company's stationary as unpersonable as their business.[BLACK LETTERS]";
	if the noun is open:
		say "[RED LETTERS]You scan the page for today's entry.  Whoever owns this planner is going sailing this evening.  Could this be the kind of exotic good time that Celine would have invited you on?[BLACK LETTERS]";

Carry out examining the grey day planner:
	if the scribbled note is read:
		say "[RED LETTERS]You compare the number on the cover of the grey planner with the one on the paper you found in your pocket, and your heart leaps as you match the digits.  This is Celine's planner alright!  Which means you now have her address and phone number.  Kind of creepy, perhaps, but she did invite you over, after all.  You run a hand through your hair, brush another futilely over the wrinkles in your jacket, and stride out the door.[BLACK LETTERS]";
		now RomScene1 is solved_romance;
		try fastforwarding instead;
	if the noun is closed:
		say "[RED LETTERS]The cover reads [line break]Official Employee Planner for Business and Personal Life Events[line break]Property of Charles & Charles Accounting LLC.[line break]Employee Details:   8572 Bromeliad Road — 525-423-9820'[line break]  The typewritten details were no use to you without a name to pin them to.[BLACK LETTERS]" instead;
	if the noun is open:
		say "[RED LETTERS]You leaf through the planner to find that the pencilled in details that the owner of this planner is holding a party at their home this very night.  It sounds fun, but you can't risk gate crashing if it's not Celine's house.[BLACK LETTERS]" instead;

After taking your crumpled jacket:
	try wearing the noun instead.
	
Carry out wearing your crumpled jacket:
	say "[RED LETTERS]As you pull on your rumbled old jacket, you hear something crinkle in the front pocket.[BLACK LETTERS]";
	now the player is carrying the scribbled note.
	
Carry out examining the scribbled note:
	now the scribbled note is read;
	
Check utilizing the pint glass:
	try drinking the pint glass instead.

Carry out drinking the pint glass:
	say "[BLUE LETTERS]Now we're talking.  A drink was just the thing I needed. [RED LETTERS]You find nothing but dust and air at the bottom of this glass, however.[BLACK LETTERS]" instead.
	
Check utilizing the computer:
	say "[RED LETTERS]The screen remains blank.  You remember that Old Man Charles keeps the power off on weekends and holidays.  As a miser and a professional bookkeeper, no opportunity to save a penny escaped his cataract-ridden gaze.[BLACK LETTERS]" instead.

RomScene2 is room with printed name "The Park: Sunset".
Understand "The Park" and "Park" and "Noon" as RomScene2.
The description of RomScene2 is
	"[one of]
	[RED LETTERS]Bromeliad Road is just a short walk away, so you decide to head over on foot — according to Celine's planner and your watch, the party started half an hour ago!  You had been passed out in that cupboard all afternoon, apparently, as is often the case following a good Christmas party.[line break]You decide to cut through the park on your way there.  The walk is slightly shorter, and you feel that the fresh air and trees and whatnot will do your aching skull a bit of good.  Your pace is as brisk as the winter wind, and you feel your spirits perk up as you round the duck pond and begin approaching the street that will take you to Bromeliad Road.[line break]Suddenly, a horrible thought strikes you, and you stop dead in your tracks.  You didn't write down her damn address, and the door to Charles & Charles locked automatically behind you!  You keep walking for lack of anything better to do as a single bead of sweat begins to slide coldly down behind your ear.[line break]Much to your surprise and discomfort, a second emotional blow hits you like a Mancurian drunk as you round the next corner.  There, behind those trees, next to the picnic benches, it's that damn temp girl from your floor!  Molly or Maggie or whatever her name is.  She sees you as you see her, and her face lights up with girlish glee at the sight of you.  Blast it, she's coming your way, too.  You're in no mood to make small talk, but on the bright side, she might know where Celine lives...[BLACK LETTERS]
	[or]
	[RED LETTERS]The local park.  Trees, benches, paths, a slightly pungent duck pond, and one damnably excitable temp who's name you can't recall at the moment.[BLACK LETTERS][stopping]".

Carry out resetting when the location is RomScene2:
	now RomScene2 is unsolved;
	now Margaret is not introduced;

Every turn when the location is RomScene2 and Margaret is not introduced:
	try asking Margaret about "conversation_starter";
	now Margaret is introduced;

Margaret is a person in RomScene2.  Understand "Marge", "Maggie", "Molly", "Marigold", "Marilyn", "Marjory", "Margarine", and "Martleby" as Margaret.  Margaret can be introduced.  Margaret is not introduced.
The printed name of Margaret is "[one of]Margaret[or]Maggie[or]Molly[or]Marigold[or]Marge[or]Marilyn[or]Marjory[or]Margarine[or]Martleby[then at random]".
The description is "[RED LETTERS]The dumpy office temp who works on your floor.  She's sweet enough, although her hair is tangled and her teeth are crooked, and she is currently blocking your path to Celine's house, the address of which you still don't know.  She's watching you expectantly, which you find very offputting.[BLACK LETTERS]"

CelineRom is a person with printed name "Celine".  Understand "Celine" as CelineRom.
The description is "[one of][RED LETTERS]Celine.  Your new love, your amour, your cherished sweetling and the one hope that kept you going throughout the days events.[BLACK LETTERS][or][RED LETTERS]God, she's great, isn't she?[BLACK LETTERS][stopping]"

RomScene3a is room with printed name "Celine's Bedroom".
Understand "Celine's Bedroom" as RomScene3a.
The ending_text of RomScene3a is "[one of]
	[RED LETTERS]Celine guides you into the center of the room, her hand on your wrist, and your mind continues to wonder at how you could have got so lucky.  Perhaps this is all a trick, and she's actually going to murder you?  That would seem more in line with your luck of late.[BLACK LETTERS]
	[or]
	[RED LETTERS]You think back to all good times you spent with Celine tonight.  She laughed at your jokes, and you at hers, you kept eachother enthralled with various anecdotes, you even teased [the printed name of Margaret] behind her back together.  And now Celine is turning off the lamp in the corner, her back to you, the cut of her dress revealing her smooth arc of her shoulder blades and the back of her long, shapely neck.[BLACK LETTERS]
	[or]
	[RED LETTERS]You're practically immobilized by her beauty, but Celine doesn't seem to mind.  She plants the first kiss on your lips, the next on your neck.  You try to think of something smart to say, but she knows what you're doing and places a slender finger against your lips.  Mutely, you allow her to pull you down onto the bed and into her embrace.[BLACK LETTERS]
	[stopping]".
The ending_turns of RomScene3a is 3.
RomScene3a is ending_room.
The description of RomScene3a is "[one of]
	[RED LETTERS]You're not entirely sure how you came to find yourself alone with Celine in her bedroom, the blinds drawn and the lights turned down low.  You feel a little bad about ditching poor old [the printed name of Margaret] after she showed you the way to Celine's house, but you hardly regret it.  That little cruelty filled you with a rush of confidence that carried through the entire evening, and now, somehow, it has brought you here.[BLACK LETTERS]
	[or]
	[RED LETTERS]Celine's bedroom.  The lights are dim, and the bed looks very comfortable.  [BLUE LETTERS]Big enough for two, as well.  Or three.[BLACK LETTERS][stopping]".

Carry out resetting in RomScene3a:
	now CelineRom is in RomScene3a;

The bed is a thing in RomScene3a.  It is fixed in place.
The description is "[RED LETTERS]Celine's bed.  It's big, white, and incredibly inviting looking.  You try not to image what she must look like sleeping in it, but do so anyway.[BLACK LETTERS]"

The nightstand is a supporter in RomScene3a.
The description is "[RED LETTERS]A simple oaken nightstand.[BLACK LETTERS]"

The alarm clock is a thing on the nightstand.
The description is "[RED LETTERS]You hate alarm clocks, but this one is Celine's, and somehow that makes you like it.  Love it, even.  Celine's great, isn't she? you sigh to yourself.  You're weird.[BLACK LETTERS]"

The book is a thing on the nightstand.
The description is "[RED LETTERS]Celine's bedtime reading.  It's a weighty tome on some French war or other — she's smart as well as beautiful.  God, you love her.[BLACK LETTERS]"

RomScene3b is room with printed name "Celine's Driveway".
Understand "Celine's Driveway" as RomScene3b.
RomScene3b is ending_room.
The ending_text of RomScene3b is "[one of]
	[RED LETTERS]The evening has passed like a blur, a long streak of misfortune that made you queasy to look at it.  You look at Celine and try to make out the words that she's saying, but all you can hear are the brief moments of conversations you had earlier, awkward and aborted.  You'd tried your best to be kind and polite and interested in everything she said, but she never seemed to find your answers especially interesting, and kept slipping off to talk to someone else.  Why hadn't you hit it off like you did last night?  What on Earth could have happened?[BLACK LETTERS]
	[or]
	[RED LETTERS]'I'm so glad you could come,' Celine is saying now, 'It was lovely to see you outside of work. '  You wonder why, if that's the case, she showed so little interest in you all evening.  'I'm so glad we're friends,' she continues.  'You're such a nice guy.'  Ah.  There it is.[BLACK LETTERS]
	[or]
	[RED LETTERS]You each say your goodbyes, Celine steps back into her house, and the door shuts in your face.  You're left in the cold, staring at the woodwork on the front door of another girl you let slip through your fingers.  You start the long walk home without complaint like the nice guy you are.  As you walk, you wonder what kind of tie you'll wear to work tomorrow.[BLACK LETTERS]
	[stopping]".
The ending_turns of RomScene3b is 3.
The description of RomScene3b is "[one of]
	[RED LETTERS]You find yourself standing on Celine's front step saying your goodbyes without much of an idea as to how you got there.    You glance around and take in the dark sky, the gravel pathway, the old red brickwork that makes up the front of Celine's house.  You'd rather look at the architecture than Celine, despite her beauty.  You know in your bones that to look at Celine is to look at what you have lost.[BLACK LETTERS]
	[or]
	[RED LETTERS]Celine's driveway.  It's dark outside, and it doesn't seem like Celine will let you back into the warmth and light of her front room.[BLACK LETTERS][stopping]".
Carry out resetting in RomScene3b:
	now CelineRom is in RomScene3b;

The bush is a thing in RomScene3b.  It is fixed in place.
The description is "[RED LETTERS]A bit of topiary running along Celine's driveway.  Shapely, though not as shapely as it's owner.[BLACK LETTERS]"

The car is a thing in RomScene3b.  It is fixed in place.
The description is "[RED LETTERS]Celine's car, a sleek new two-door with great mileage.  As cars go it's stylish, classy, intelligent, and utterly out of your reach — much like Celine herself, apparently.[BLACK LETTERS]"


Chapter V - Game Setup

The player is in Intro.

When play begins:
	now genre_value is 0;
	say "[BLUE LETTERS]I woke up in a haze, at that moment - memory wasn't exactly my strong suit. Quite a shame, when you're in the type of business I'm in, your memory is your only friend. My head was pounding, was I hit? Did someone ambush me? Where was I even? It was dark, that's for sure. The kind of jarring, confusing darkness that encrouches on your and sucks every bit of your breath like a void. A void, huh? Reminds me of my most recent case, Celine - a vicious, femme fatale known in twelve states for triple homicides. One can only imagine the void that lives inside that girl's heart, but that ain't my problem. My problem is bringing her whereabouts to light because that's who I am, a PI, the only light that can deshroud this dark, dank city. Speaking of light, about time I grab that trusty lighter of mine and figure out where I am.[BLACK LETTERS]";
	center "[line break]Press SPACE key to begin.";
	wait for the SPACE key;
	say "[line break][line break] [RED LETTERS]I'm not sure about that at all.  No, let's try that again.  How about this: you awake to find yourself in the dark — not in a figurative dark, of course, for that would be nothing out of the ordinary — but a real heavy, stifling dark, thick and treacly enough to cut with knife and serve with a lump of hot butter.  Your thoughts swim as your hands grope blindly.  You brain seems to be operating in low gear as you search for your last clear memory.  Blurred lights and swirling colors solidify in your mind into a face:  Celine!  That charming girl from human resources that you met at the office Christmas party.  You hit it off, she smiled that sweet smile, and she told you to meet her tonight at — where was it again?  And for that matter, where the blazes were you?";
	center "[line break][RED LETTERS]Alright, that sounds a bit better.  Carry on.[BLACK LETTERS]";
	center "[line break]Press SPACE key to begin."; 
	wait for the SPACE key;
	clear the screen;

Chapter VI - Action Defaults

[These defaults have to be at the end to avoid overriding other actions.]

Check utilizing:
	say "I can't use [the noun]." instead.

Check using: 
	say "I can't use those things like that." instead.
