"The Quarrel" by Zane Mariano & Jon Gill

Include Basic Screen Effects by Emily Short.
Include Reactable Quips by Michael Martin.
Release along with an interpreter, the source text and a website.
Use no scoring.

Chapter I - Values

[Data]
Genre_value is a number that varies.

Chapter II - Actions
	
[FASTFORWARD Action]	
Fastforwarding is an action applying to nothing.
Understand "do_fastforward" as fastforwarding.

move_to_room is a room that varies.
Instead of fastforwarding:
	let is_next_room be false;
	if location is solved_noir:
		let is_next_room be whether or not there is a next_room_noir corresponding to a curr_room of the location in the table of room connections;
		if is_next_room is true:
			now move_to_room is the next_room_noir corresponding to a curr_room of the location in the table of room connections;
		otherwise:
			say "Nowhere to go.";
	otherwise if location is solved_romance:
		let is_next_room be whether or not there is a next_room_romance corresponding to a curr_room of the location in the table of room connections;
		if is_next_room is true:
			now move_to_room is the next_room_romance corresponding to a curr_room of the location in the table of room connections;
		otherwise:
			say "Drat!  There's nowhere for you to go.";
	otherwise:
		say "There's still work to be done here.";
	if is_next_room is true:
		move the player to move_to_room, without printing a room description;
		try resetting;
		move the player to move_to_room;
		

[REWIND Action]
Rewinding is an action applying to nothing.
Understand "rewind" as rewinding.

prev_room_trans is text that varies.
Instead of rewinding:
	let is_prev_room be false;
	let is_prev_room be whether or not there is a prev_room corresponding to a curr_room of the location in the table of room connections;
	if is_prev_room is true:
		say the description corresponding to a curr_room of the location in the table of rewind descriptions;
		if the player consents:
			now move_to_room is the prev_room corresponding to a curr_room of the location in the table of room connections;
			move the player to move_to_room, without printing a room description;
			try resetting;
			move the player to move_to_room;
	otherwise:
		say "This is where it all began…";
	

[RESET Action]
Resetting is an action applying to nothing.
Understand "test_reset" as resetting.
	
[Override RESET for each specific room.]


[Ending the game]
Ending_text_room is a room that varies.
After resetting when the location is ending_room:
	now ending_text_room is the location;
	the fat lady sings in the ending_turns of the location - 1 turns from now.

Every turn when the location is ending_text_room:
	say "[the ending_text of ending_text_room]";
	
At the time when the fat lady sings:
	end the game saying "THE END".
	

[--------------- Player Action Descriptions ---------------]


[UTILIZE]
[Utilize is the version of Use that applies to a single object]
Utilizing is an action applying to one visible thing.
Understand "utilize [something]" as utilizing.
Understand "use [something]" as utilizing.

[USE]
[Use affects one object with another]
Using is an action applying to two visible things.
Understand "use [something] on [something]" as using.
Understand "use [something] with [something]" as using.
Understand "apply [something] to [something]" as using.

[HELP]
Helping is an action applying to nothing. Understand "help" as helping.
After helping when genre_value is less than 0:
	say "[BLUE LETTERS] Time to EXAMINE myself.  Really take a deep LOOK inside.  I was a hard man, with hard convictions.  I'd USE and TAKE, but I'd rarely ASK before I did.[BLACK LETTERS]";
After helping when genre_value is 0:
	say "This is generic help text.";
After helping when genre_value is greater than 0:
	say "This is romantic help text.";


[EXAMINE ME]

Instead of examining player when genre_value is less than 0: say "Noir description."
Instead of examining player when genre_value is 0: say "Generic description."
Instead of examining player when genre_value is greater than 0: say "Romance description."


[GO N/S/E/W]

Instead of going when genre_value is less than 0: say "[BLUE LETTERS]Yes, part of me wanted to run.  But this was where I had to be. [BLACK LETTERS]".
Instead of going when genre_value is 0: say "[RED LETTERS]Oh, bother!  You still have [BLUE LETTERS]work to do.  Messy work.[BLACK LETTERS]".
Instead of going when genre_value is greater than 0: say "Romance going."

[Instead of doing something other than looking, examining, asking, telling, or going, say "HAHAHA NO."]

Chapter III - Conversations


[TALKING Action]

Talking is an action applying to one thing.
Understand "talk [something]" and "talk to [something]" as talking.
Check talking: try asking the noun about "the_default".


[--------------- Conversation Content -- Can be continued in other Chapters ---------------]


	Table of Ask Results (continued)
  	NPC  		topic  					result  
  	Charles	  	"his face" or "ugliness"			hate my face
	Charles		"the_default"				want to die

	Table of Tell Results (continued)
  	NPC  		topic			  		result  
  	Charles  		"sucks"				  	hate my face

	Table of Quip Texts (continued)
  	quip  		quiptext  
  	hate my face	"Yeah, it kind of sucks.  I'm pretty hideous."
	want to die	"You're right.  I should kill myself."

	Table of Quip Followups (continued)
  	quip  		option  					result  
  	hate my face  	"Life isn't really worth living for you."		want to die
  	hate my face  	"You're freaking hideous man."  		want to die

[Quip Results]

	After quipping when the current quip is want to die: end the story saying "Charles goes off to hang himself.  Rough.".

Charles is a person.  The knife is an object.

Chapter IV - Rooms

[Room state]
A room can be unsolved, solved_noir, or solved_romance.  A room is usually unsolved.
Rooms have a text called ending_text.
Rooms have a number called ending_turns.
Rooms can be ending_room. A room is usually not ending_room.

[Room Relations]
	Table of Room Connections
	curr_room		next_room_noir	next_room_romance		prev_room
	Intro			NoirScene1		RomScene1				--
	NoirScene1		NoirScene2		--						Intro
	NoirScene2		NoirScene3a		NoirScene3b				NoirScene1
	RomScene1		--				RomScene2				Intro
	RomScene2		RomScene3b		RomScene3a				RomScene1				
	
	Table of Rewind Descriptions
	curr_room		description
	Intro			--
	NoirScene1		"TEMP REWIND FROM NOIRSCENE1 TEXT"
	NoirScene2		"TEMP REWIND FROM NOIRSCENE2 TEXT"
	RomScene1		"[RED LETTERS]Wait, don't — [BLUE LETTERS]I shook the quaint, weirdly English daydream from my head.  Clearly I'd been hitting the pills too hard again.  Now, where was I?[BLACK LETTERS]"
	RomScene2		"[RED LETTERS]That's not quite how it happened, though, was it?  Let's try that last bit again…[BLACK LETTERS]"

[Intro Scene]
Intro is unsolved.

Intro is a room with printed name "The Closet: Trapped".
Understand "The Closet" and "Closet" as Intro.

The description of Intro is "[one of]
	[BLUE LETTERS]You find yourself locked in a closet. You'd be surprised how often someone like yourself finds themselves in these sort of situations after dealing with gangsters and thugs too soft to land the killing blow. [RED LETTERS]That's a bit rich.  More often you find yourself in situations like this when Jenkins from third floor locks the door behind you while you're looking for a fresh pen.[BLUE LETTERS] They'd just as soon lock you away and hope the chemical fumes finish the job. Too bad for them, because escaping locked rooms was your speciality. [RED LETTERS] It certainly wasn't your work.  'Tardy, easily distracted, and poor with numbers,' your last performance review had read. [BLUE LETTERS]The closet was an archive closet where your secretary would store all the old case files from your brilliant triumphs and utter failures. [RED LETTERS] Actually, it smelled more like the janitorial supply closet.  Lovely. [BLUE LETTERS]Don't bother trying the door-knob though, it was [RED LETTERS]locked tight.[BLACK  LETTERS]
	[or][BLUE LETTERS]The closet is dark and stifling as a coffin,[RED LETTERS] with a wet, vaguely antiseptic smell about it. [BLACK LETTERS][stopping]"

Carry out resetting in Intro:
	[----RomItems----]
	now the filing cabinet is closed;
	now the filing cabinet is not pushed;
	now the crumpled paper is off-stage;
	now the crumpled paper is not written on;
	now the basket is in Intro;
	now the mop is in Intro;
	[----NoirItems----]
	now Boots is in Intro;
	now Flimsy Doorknob is in Intro;
	now Gun Safe is in Intro;
	now Loaded Gun is in Gun Safe;
	
The filing cabinet is a container in Intro. 
The filing cabinet is not portable.
The filing cabinet is closed and openable.
The filing cabinet can be pushed.  The filing cabinet is not pushed.
Carry out examining filing cabinet:
	if filing cabinet is closed, say "[BLUE LETTERS]Closed, like the door to this closet.[BLACK LETTERS]" instead;
	if filing cabinet is open, say "[BLUE LETTERS]Nothing in here but my hopes and dreams.  Which is to say, nothing in here.[BLACK LETTERS]". 
Instead of pulling filing cabinet:
	say "[RED LETTERS]Despite your thrice-weekly squash practices, you are unable to get a strong enough grip to pull the cabinet towards you.[BLACK LETTERS]" instead.
Instead of pushing filing cabinet:
	if the filing cabinet is not pushed:
		say "[RED LETTERS]You set your back against the cabinet, brace your legs against the wall, and give it the old college try.  You think you feel a blood vessel pop in your forehead, but you succeed in moving the cabinet a few inches.  As you regather your breath, you notice that a small piece of paper is sticking out from behind the cabinet.[BLACK LETTERS]";
		now the crumpled paper is in Intro;
		now the filing cabinet is pushed;
	otherwise:
		say "[RED LETTERS]You give the cabinet another push, wondering what else you might find behind it.  A tenner maybe?  Despite your hopes, though, the cabinet refuses to yield further ground.[BLACK LETTERS]".
	
The basket is a container in Intro.
The description of the basket is "[RED LETTERS]Although it was intended to store pens, it has long-since been emptied by the sinister hand of petty corporate theft.[BLACK LETTERS]"
	
A crumpled paper is a thing.
The description of the crumpled paper is "A crumpled sheet of paper".
The paper can be written on.  The paper is not written on.
Carry out examining crumpled paper:
	if crumpled paper is not written on:
		say "[RED LETTERS]Somebody's typewritten notes.  Some rot about a case or a dame or something.  Had Jenkins been working on his novel during work hours again?[BLACK LETTERS]" instead;
	otherwise:
		say "[RED LETTERS]A ratty piece of paper with the word HELP scrawled across it in stinking mop goo.[BLACK LETTERS]" instead.

The dirty mop is a thing in Intro.
The description of the mop is "[RED LETTERS]The janitor's mop.  Ragged, splintery, and covered in some kind of viscous black goop."
Carry out taking the mop:
	say "[RED LETTERS]You stifle a yelp as a splinter a centimeter long embeds itself in your palm.[BLACK LETTERS]."
	
Goop is a part of the mop.  Understand "slime" and "goo" and "gunk" as goop.
The description of the goop is "[RED LETTERS]An disgusting, inky slime coating the fibers of the mop.[BLACK LETTERS]".

Check using:
	if the noun is mop:
		try utilizing the noun instead;
	if the noun is goop and the second noun is crumpled paper:
		now crumpled paper is written on;
		say "[RED LETTERS]You suppress a shudder as you rub the tarry substance over the paper.  When you are done your hands are stained and the word HELP is scrawled across the sheet, crude and malodrous.[BLACK LETTERS]" instead;
	if the noun is crumpled paper and the second noun is crack:
		if crumpled paper is not written on:
			say "[RED LETTERS]You begin to slide the paper under the door, but realize that sharing someone else's notes with the wider world will not do much to aid you in your current predicament.  You withdraw the paper from the crack.[BLACK LETTERS]" instead;
		otherwise:
			say "[RED LETTERS]You slide your desperate missive through the crack beneath the door.  After a few minutes, you hear someone walk towards the door, pick up the note, and pause.  A few seconds later, the latch clicks, the door opens, and you are blinded by the bright light that spills into the closet from outside.[BLACK LETTERS]";
			now Intro is solved_romance;
			try fastforwarding instead;
	if the noun is crumpled paper and crumpled paper is not written on:
		try utilizing crumpled paper instead;

Check utilizing:
	if the noun is mop:
		say "[RED LETTERS]There's nothing to clean.  Besides, that goop on the end of it would probably leave more of a mess than was already there.[BLACK LETTERS]" instead;
	if the noun is crumpled paper:
		say "[BLUE LETTERS]Just ancient history now.[BLACK LETTERS]";
				
The closet door is an object in Intro.
The description of the closet door is "[RED LETTERS]Heavy, wooden, entirely unyielding.  Much like your boss when you asked for that raise. Light spills in through a tiny crack along the bottom of it.[BLACK LETTERS]".
Carry out pushing the closet door: say "[RED LETTERS]You press your shoulder against the wood and heave.  You achieve nothing more than a stiff back.[BLACK LETTERS]" instead.
Carry out opening the closet door: say "[RED LETTERS]You give the handle a frustrated jiggle, but it is definitely locked.[BLACK LETTERS]" instead.

The crack is part of the closet door.
Carry out examining the crack: say "[RED LETTERS]It's wide enough to slip something through, like a note or ribald birthday card.[BLACK LETTERS]" instead.

Check attacking closet door:
	say "[RED LETTERS]You would, but you don't want to bruise your hands.  That door looks hard.[BLACK LETTERS]" instead.

[ ----- Noir Solution for Intro ----- ]
	
Boots are a thing in Intro. The description is "BOOTS TEMP DESCRIPTION".
Flimsy Doorknob is a thing in Intro. The description is "DOORKNOB TEMP DESCRIPTION".
Gun Safe is a container in Intro. The description is "GUN SAFE TEMP DESCRIPTION".
Gun Safe is openable, closed, opaque and fixed in place.
Loaded Gun is a thing in Gun Safe. The description is "LOADED GUN TEMP DESCRIPTION".

Check using: 
	if the noun is Boots and the second noun is Flimsy Doorknob, 
		say "TEXT FOR SOLVING PUZZLE WITH BOOTS ON DOORKNOB";
		now Intro is solved_noir;
		try fastforwarding;
	if the noun is Loaded Gun and the second noun is Flimsy Doorknob,
		say "TEXT FOR SOLVING PUZZLE WITH LOADED GUN ON DOORKNOB";
		now Intro is solved_noir;
		try fastforwarding;

	
[Noir Scenes]
NoirScene1 is a room with printed name "PI Office".
Understand "PI Office" as NoirScene1.
The description of NoirScene1 is "[BLUE LETTERS]Ahhh, the old office. This place never lacked the charm that you loved. It was cozy, dimly light and always had a slight, frigid bite that kept your nerves a bit on edge - just the way you like it. The fan overhead cut through the dry air with every beat of your own black heart leaving shadows dancing around the room from the light that hung just above it. It was time you got to business, finding Celene. Last you remember, you had three vital pieces of information on her. Unfortunately, after coming onto your secretary last week, she hasn't been back to clean up your mess. Who knows where those files could be, better start looking.[BLACK LETTERS]."

NoirScene2 is a room with printed name "The Park: Midnight".
Understand "The Park" and "Park" and "Midnight" as NoirScene2.
The description of NoirScene2 is "[BLUE LETTERS]It's always sad when she gets the young ones. Staring at the most recent victim underneath this star-less nights, you can't help but look deeply into her cold, lifeless eyes  forcing you into a reflective trance. How many girls had it been, Celene? 20? 30? Will your bloodthirst never stop? This young beauty must have had the world open to her, so full of promise and you struck her down - for what? You could still smell Celene's infamous perfume in the air; she can't be far now - it's time to end this, if only you could find some clue to her whereabouts.[BLACK LETTERS]".

NoirScene3a is a room with printed name "Victim's House".
Understand "Victim's House" as NoirScene3a.
The description of NoirScene3a is "[BLUE LETTERS]You could feel it, on the otherside of the door - everything you worked for, it all culminated to what was behind that door, Celene. You had finally tracked her down and it was time for the final showdown. Taking one last deep breath, you open the door and bathed in the stark, white light that flooded the room. [line break][line break]'The Jig is up, Celene.' you claimed, brandishing your trusty six-shooter towards Celene. [line break][line break]She laughs at your petty attempts to arrest her, 'Really? The jig is up? Please, do you honestly think I wasn't prepared for you? You follow someone for this long, you know they're going to notice.' You could see her own weapon pointed straight at that sweet spot between your eyes. Your eyes couldn't help but focus down the barrel, how many poor girls had seen this same scene? [line break][line break]'I see. So I guess we have ourselves a stand-off. So tell me, Celene - why did you do it? Why so many young ladies? What did they do wrong?' your questions cut through her like razor blades of ice and it was obvious.   [line break][line break]'What did they do WRONG!? You blithering idiot, you've chased my tail for ten years and you still haven't figured that much out?' her voice was raspt, wicked with hatred. [line break][line break]'Ten years is a long time Celene. Ever since you left my bed that morning and never came back - you changed. I don't even remember who you are anymore.' you said, each word filled with memories of passion, now tinted with disgust. [line break][line break]'It is a long time. You never got over me, did you? Still you lay up at night, longing for my kiss, my touch. I can see it in your hands even as you hold that pistol. You don't have the nerve to shoot me. You'll die a sad man, never knowing what happened to your wife or why she became the greatest serial killer this world had ever seen.' she finished, her thin lips now forming into a wicked smile. It was true, everything she said. You couldn't help but notice your right-hand, gripping the pistol so tightly it shaked with fear and cowardice. But before you even had time to react, a loud bang punctuated the night. It was all over. [line break][line break]You could hear her high-heels click and clack behind you as the door closed to not only end this chapter on your life, but the whole book.[BLACK LETTERS]".

NoirScene3b is a room with printed name "Celine's Hotel".
Understand "Celine's Hotel" as NoirScene3b.
The description of NoirScene3b is "THIS IS A TEMP DESCRIPTION!!!".


[Romance Scenes]
RomScene1 is room with printed name "The Offices of Charles and Charles, Accounting LLC".
Understand "Accounting Office" as RomScene1.
The description of RomScene1 is "THIS IS A TEMP DESCRIPTION!!!".

Carry out resetting when the player is in RomScene1: 
	now Charles is in RomScene1;
	now genre_value is 1.

RomScene2 is room with printed name "The Park: Noon".
Understand "The Park" and "Park" and "Noon" as RomScene2.
The description of RomScene2 is "THIS IS A TEMP DESCRIPTION!!!".

RomScene3a is room with printed name "Celine's Bedroom".
Understand "Celine's Bedroom" as RomScene3a.
The ending_text of RomScene3a is "[one of]Oh my[stopping]".
The ending_turns of RomScene3a is 1.
RomScene3a is ending_room.
The description of RomScene3a is "THIS IS A TEMP DESCRIPTION!!!".

RomScene3b is room with printed name "Celine's Driveway".
Understand "Celine's Driveway" as RomScene3b.
RomScene3b is ending_room.
The ending_text of RomScene3b is "[one of]Oh my[stopping]".
The ending_turns of RomScene3b is 1.
The description of RomScene3b is "THIS IS A TEMP DESCRIPTION!!!".


Chapter V - Game Setup

The player is in Intro.

When play begins:
	now genre_value is 0;
	say "[BLUE LETTERS]I woke up in a haze, at that moment - memory wasn't exactly my strong suit. Quite a shame, when you're in the type of business I'm in, your memory is your only friend. My head was pounding, was I hit? Did someone ambush me? Where was I even? It was dark, that's for sure. The kind of jarring, confusing darkness that encrouches on your and sucks every bit of your breath like a void. A void, huh? Reminds me of my most recent case, Celine - a vicious, femme fatale known in twelve states for triple homicides. One can only imagine the void that lives inside that girl's heart, but that ain't my problem. My problem is bringing her whereabouts to light because that's who I am, a PI, the only light that can deshroud this dark, dank city. Speaking of light, about time I grab that trusty lighter of mine and figure out where I am.[BLACK LETTERS]";
	center "[line break]Press SPACE key to begin.";
	wait for the SPACE key;
	say "[line break][line break] [RED LETTERS]I'm not sure about that at all.  No, let's try that again.  How about this: you awake to find yourself in the dark — not in a figurative dark, of course, for that would be nothing out of the ordinary — but a real heavy, stifling dark, thick and treacly enough to cut with knife and serve with a lump of hot butter.  Your thoughts swim as your hands grope blindly.  You brain seems to be operating in low gear as you search for your last clear memory.  Blurred lights and swirling colors solidify in your mind into a face:  Celine!  That charming girl from marketing that you met at the office Christmas party.  You hit it off, she smiled that sweet smile, and she told you to meet her tonight at — where was it again?  And for that matter, where the blazes were you?";
	center "[line break][RED LETTERS]Alright, that sounds a bit better.  Carry on.[BLACK LETTERS]";
	center "[line break]Press SPACE key to begin."; 
	wait for the SPACE key;
	clear the screen;

Chapter VI - Action Defaults

[These defaults have to be at the end to avoid overriding other actions.]

Check utilizing:
	say "I can't use [the noun]." instead.

Check using: 
	say "I can't use those things like that." instead.
